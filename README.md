# V Documenta√ß√£o

[Tradu√ß√£o da documenta√ß√£o original](https://github.com/vlang/v/blob/master/doc/docs.md)

### Introdu√ß√£o

**V** √© uma linguagem de programa√ß√£o compilada estaticamente tipada projetada para construir software sustent√°vel.

√â semelhante ao Go e seu design tamb√©m foi influenciado por Oberon, Rust, Swift, Kotlin e Python.

**V** √© uma linguagem muito simples. Analisar esta documenta√ß√£o levar√° cerca de uma hora e, ao final dela, voc√™ ter√° aprendido praticamente toda a linguagem.

A linguagem promove a escrita de c√≥digo simples e claro com abstra√ß√£o m√≠nima.

Apesar de ser simples, **V** d√° ao desenvolvedor muito poder. Tudo o que voc√™ pode fazer em outras linguagens, voc√™ pode fazer em **V**.

### Instalar da fonte

A principal forma de obter o melhor e mais recente **V** √© instal√°-lo a partir do c√≥digo-fonte . √â f√°cil e geralmente leva apenas alguns segundos .

### Linux, macOS, FreeBSD, etc:

Voc√™ precisa git, e um compilador C como tcc, gccou clang, e make:

```shell
git clone https://github.com/vlang/v
cd v
make
```

### Windows

Voc√™ precisa git, e um compilador C como tcc, gcc, clang ou msvc:

```shell
git clone https://github.com/vlang/v
cd v
make.bat -tcc
```

NB: Voc√™ tamb√©m pode passar um dos -gcc, -msvc, -clangao make.batinv√©s, se voc√™ preferir usar um compilador C diferente, mas -tcc √© pequeno, r√°pido e f√°cil de instalar (V vai baixar um bin√°rio pr√©-constru√≠dos automaticamente).

Recomenda-se adicionar esta pasta ao PATH de suas vari√°veis de ambiente. Isso pode ser feito com o comando ```v.exe symlink```.

### Android

A execu√ß√£o de aplicativos gr√°ficos V no Android tamb√©m √© poss√≠vel via [vab](https://github.com/vlang/vab).

V Depend√™ncias do Android: V , Java JDK > = 8, Android SDK + NDK .

1. Instale depend√™ncias (veja [vab](https://github.com/vlang/vab))
2. Conecte seu dispositivo Android
3. Run:

```shell
git clone https://github.com/vlang/vab && cd vab && v vab.v
./vab --device auto run /path/to/v/examples/sokol/particles
```

Para obter mais detalhes e solu√ß√£o de problemas, visite o [reposit√≥rio vab GitHub](https://github.com/vlang/vab).

### √çndice

- Ol√° Mundo
- Executando uma pasta de projeto
- Coment√°rios
- Fun√ß√µes
	- Retornando v√°rios valores
- Visibilidade do s√≠mbolo
- Vari√°veis
- Tipos
	- Strings
	- N√∫meros
	- Arrays
	- Matrizes de tamanho fixo
	- Mapas
- Importa√ß√µes de m√≥dulos
- Declara√ß√µes e express√µes
	- If
	- In operator
	- For loop
	- Math
	- Defer
- Structs
	- Estruturas embutidas
	- Valores de campo padr√£o
	- Sintaxe literal curta de estrutura
	- Modificadores de acesso
	- M√©todos
- Unions
- Fun√ß√µes 2
	- Fun√ß√µes puras por padr√£o
	- Argumentos mut√°veis
	- N√∫mero vari√°vel de argumentos
	- Fun√ß√µes an√¥nimas e de alta ordem
- Refer√™ncias
- Constantes
- Fun√ß√µes integradas
- Impress√£o de tipos personalizados
- M√≥dulos
- Tipos 2
	- Interfaces
	- Enums
	- Tipos de soma
	- Tipos de op√ß√£o / resultado e tratamento de erros
- Gen√©ricos
- Simultaneidade
	- Gerando Tarefas Simult√¢neas
	- Canais
	- Objetos Compartilhados
- Decodificando JSON
- Testando
- Gerenciamento de mem√≥ria
- ORM
- Escrevendo documenta√ß√£o
- Ferramentas
	- v fmt
	- Profiling
- T√≥picos Avan√ßados
	- C√≥digo inseguro de mem√≥ria
	- Structs com campos de refer√™ncia
	- sizeof e __offsetof
	- Chamando C de V
	- Depura√ß√£o de c√≥digo C gerado
	- Compila√ß√£o condicional
	- Pseudo vari√°veis em tempo de compila√ß√£o
	- Reflex√£o em tempo de compila√ß√£o
	- Sobrecarga limitada do operador
	- Inline assembly
	- Traduzindo C para V
	- Recarregando c√≥digo quente
	- Compila√ß√£o cruzada
	- Scripts de shell de plataforma cruzada em V
	- Atributos
	- Goto
- Ap√™ndices
	- Palavras-chave
	- Operadores

### Ol√° Mundo

```vlang
fn main() {
	println('hello world')
}
```

Salve este trecho em um arquivo chamado ```hello.v```. Agora fazer: ```v run hello.v```.

>Isso pressup√µe que voc√™ tenha vinculado simbolicamente seu V com v symlink, conforme descrito aqui . Se ainda n√£o o fez, voc√™ deve digitar o caminho para V manualmente.

Parab√©ns - voc√™ acabou de escrever e executar seu primeiro programa em V!

Voc√™ pode compilar um programa sem execu√ß√£o com ```v hello.v```. Consulte ```v help``` para todos os comandos suportados.

No exemplo acima, voc√™ pode ver que as fun√ß√µes s√£o declaradas com a ```fn palavra-chave```. O tipo de retorno √© especificado ap√≥s o nome da fun√ß√£o. Nesse caso main, n√£o retorna nada, portanto, n√£o h√° tipo de retorno.

Como em muitas outras linguagens (como C, Go e Rust), main √© o ponto de entrada do seu programa.

println √© uma das poucas fun√ß√µes integradas. Ele imprime o valor passado para a sa√≠da padr√£o.

A declara√ß√£o ```fn main()``` pode ser ignorada em programas de um arquivo. Isso √© √∫til ao escrever pequenos programas, "scripts" ou apenas aprender o idioma. Para resumir, ```fn main()``` ser√° ignorado neste tutorial.

Isso significa que um programa "hello world" em V √© t√£o simples quanto

```vlang
println('hello world')
```

### Executando uma pasta de projeto com v√°rios arquivos

Suponha que voc√™ tenha uma pasta com v√°rios arquivos ```.v```, onde um deles cont√©m sua fun√ß√£o ```main()``` e os outros arquivos t√™m outras fun√ß√µes auxiliares. Eles podem ser organizados por t√≥pico, mas ainda n√£o estruturados o suficiente para serem seus pr√≥prios m√≥dulos reutiliz√°veis separados, e voc√™ deseja compil√°-los todos em um programa.

Em outras linguagens, voc√™ teria que usar includes ou um sistema de constru√ß√£o para enumerar todos os arquivos, compil√°-los separadamente para arquivos objeto e, em seguida, vincul√°-los em um execut√°vel final.

No V, entretanto, voc√™ pode compilar e executar toda a pasta de arquivos ```.v``` juntos, usando apenas ```v run .```. Passar par√¢metros tamb√©m funciona, ent√£o voc√™ pode fazer: v run . --yourparam some_other_stuff

O exemplo acima ir√° primeiro compilar seus arquivos em um √∫nico programa (com o nome de sua pasta/projeto) e, em seguida, ir√° executar o programa --yourparam some_other_stuff transmitido a ele como par√¢metros CLI.

Seu programa pode ent√£o usar os par√¢metros CLI como este:

```vlang
import os

println(os.args)
```

NB: ap√≥s uma execu√ß√£o bem-sucedida, V ir√° deletar o execut√°vel gerado. Se voc√™ quiser mant√™-lo, use ```v -keepc run .```, ou apenas compile manualmente com ```v .```.

NB: qualquer sinalizador do compilador V deve ser passado antes do runcomando. Tudo ap√≥s o arquivo/pasta de origem, ser√° passado para o programa como est√° - n√£o ser√° processado por V.

### Coment√°rios

```vlang
// Este √© um coment√°rio de uma √∫nica linha.
/*
 Este √© um coment√°rio de v√°rias linhas.
	/* Pode ser aninhado. */
*/
```

### Fun√ß√µes

```vlang
fn main() {
	println(add(77, 33))
	println(sub(100, 50))
}

fn add(x int, y int) int {
	return x + y
}

fn sub(x int, y int) int {
	return x - y
}
```

Novamente, o tipo vem depois do nome do argumento.

Assim como em **Go** e **C**, as fun√ß√µes n√£o podem ser sobrecarregadas. Isso simplifica o c√≥digo e melhora a capacidade de manuten√ß√£o e leitura.

Fun√ß√µes podem ser usadas antes de sua declara√ß√£o: add e sub s√£o declarados ap√≥s main, mas ainda podem ser chamados de main. Isso √© verdadeiro para todas as declara√ß√µes em V e elimina a necessidade de arquivos de cabe√ßalho ou de pensar sobre a ordem dos arquivos e declara√ß√µes.

### Retornando v√°rios valores

```vlang
fn foo() (int, int) {
	return 2, 3
}

a, b := foo()
println(a) // 2
println(b) // 3
c, _ := foo() // ignore os valores usando `_`
```

### Visibilidade do s√≠mbolo

As fun√ß√µes s√£o privadas (n√£o exportadas) por padr√£o. Para permitir que outros m√≥dulos os usem, inclua pub. O mesmo se aplica a constantes e tipos.

Nota: pub s√≥ pode ser usado a partir de um m√≥dulo nomeado. Para obter informa√ß√µes sobre como criar um m√≥dulo, consulte M√≥dulos.

### Vari√°veis

```vlang
name := 'Bob'
age := 20
large_number := i64(9999999999)
println(name)
println(age)
println(large_number)
```

As vari√°veis s√£o declaradas e inicializadas com ```:=```. Essa √© a √∫nica maneira de declarar vari√°veis em V. Isso significa que as vari√°veis sempre t√™m um valor inicial.

O tipo da vari√°vel √© inferido do valor do lado direito. Para escolher um tipo diferente, use a convers√£o de tipo: a express√£o T(v) converte o valor v para o tipo T.

Ao contr√°rio da maioria das outras linguagens, V s√≥ permite definir vari√°veis em fun√ß√µes. Vari√°veis globais (n√≠vel de m√≥dulo) n√£o s√£o permitidas. N√£o h√° estado global em V (consulte Fun√ß√µes puras por padr√£o para obter detalhes).

Para consist√™ncia em diferentes bases de c√≥digo, todos os nomes de vari√°veis e fun√ß√µes devem usar o estilo snake_case, ao contr√°rio dos nomes de tipo, que devem usar PascalCase.

### Vari√°veis mut√°veis

```vlang
mut age := 20
println(age)
age = 21
println(age)
```

Para alterar o valor da vari√°vel, use ```=```. Em V, as vari√°veis s√£o imut√°veis por padr√£o. Para poder alterar o valor da vari√°vel, voc√™ deve declar√°-la com ```mut```.

Tente compilar o programa acima ap√≥s remover o ```mut``` da primeira linha.

### Inicializa√ß√£o vs atribui√ß√£o

Observe a diferen√ßa (importante) entre ```:=``` e ```=```. ```:=``` √© usado para declarar e inicializar, ```=``` √© usado para atribuir.

```vlang
fn main() {
	age = 21
}
```

Este c√≥digo n√£o ser√° compilado, porque a vari√°vel idade n√£o foi declarada. Todas as vari√°veis precisam ser declaradas em V.

```vlang
fn main() {
	age := 21
}
```

Os valores de v√°rias vari√°veis podem ser alterados em uma linha. Desta forma, seus valores podem ser trocados sem uma vari√°vel intermedi√°ria.

```vlang
mut a := 0
mut b := 1
println('$a, $b') // 0, 1
a, b = b, a
println('$a, $b') // 1, 0
```

### Erros de declara√ß√£o

No modo de desenvolvimento, o compilador ir√° avis√°-lo de que voc√™ n√£o usou a vari√°vel (voc√™ receber√° um aviso de "vari√°vel n√£o usada"). No modo de produ√ß√£o (habilitado passando o sinalizador ```-prod``` para v - ```v -prod foo.v```), ele n√£o compilar√° (como em Go).

```vlang
fn main() {
	a := 10
	if true {
		a := 20 // erro: redefini√ß√£o de `a`
	}
	// aviso: vari√°vel n√£o utilizada `a`
}
```

Ao contr√°rio da maioria das linguagens, o sombreamento vari√°vel n√£o √© permitido. Declarar uma vari√°vel com um nome que j√° √© usado em um escopo pai causar√° um erro de compila√ß√£o.

No entanto, voc√™ pode criar sombra nos m√≥dulos importados, pois isso √© muito √∫til em algumas situa√ß√µes:

```vlang
import ui
import gg

fn draw(ctx &gg.Context) {
	gg := ctx.parent.get_ui().gg
	gg.draw_rect(10, 10, 100, 50)
}
```

### Tipos

#### Tipos primitivos

```vlang
bool

string

i8    i16  int  i64      i128 (em breve)
byte  u16  u32  u64      u128 (em breve)

rune // representa um ponto de c√≥digo Unicode

f32 f64

byteptr, voidptr, charptr, size_t // estes s√£o usados principalmente para interoperabilidade C

any // semelhante ao void * de C e √† interface de Go {}
```

Observe que, ao contr√°rio de C e Go, int √© sempre um n√∫mero inteiro de 32 bits.

H√° uma exce√ß√£o √† regra de que todos os operadores em V devem ter valores do mesmo tipo em ambos os lados. Um pequeno tipo primitivo de um lado pode ser promovido automaticamente se se ajustar completamente ao intervalo de dados do tipo do outro lado. Estas s√£o as possibilidades permitidas:

```vlang
   i8 ‚Üí i16 ‚Üí int ‚Üí i64
                  ‚Üò     ‚Üò
                    f32 ‚Üí f64
                  ‚Üó     ‚Üó
 byte ‚Üí u16 ‚Üí u32 ‚Üí u64 ‚¨é
      ‚Üò     ‚Üò     ‚Üò      ptr
   i8 ‚Üí i16 ‚Üí int ‚Üí i64 ‚¨è
```

Um valor ```int```, por exemplo, pode ser promovido automaticamente para ```f64``` ou ```i64```, mas n√£o para ```u32```. (```u32``` significaria perda do sinal para valores negativos). A promo√ß√£o de ```int``` para ```f32```, no entanto, atualmente √© feita automaticamente (mas pode levar √† perda de precis√£o para valores grandes).

Literais como ```123``` ou ```4.56``` s√£o tratados de maneira especial. Eles n√£o levam a promo√ß√µes de tipo, no entanto, o padr√£o √© ```int``` e ```f64``` respectivamente, quando seu tipo precisa ser decidido:

```vlang
u := u16(12)
v := 13 + u    // v √© do tipo `u16` - sem promo√ß√£o
x := f32(45.6)
y := x + 3.14  // x √© do tipo `f32` - sem promo√ß√£o
a := 75        // a √© do tipo `int` - padr√£o para int literal
b := 14.7      // b √© do tipo `f64` - padr√£o para literal flutuante
c := u + a     // c √© do tipo `int` - promo√ß√£o autom√°tica do valor de` u`
d := b + x     // d √© do tipo `f64` - promo√ß√£o autom√°tica do valor de` x`
```

#### Strings

```vlang
name := 'Bob'
println(name.len)
println(name[0]) // a indexa√ß√£o d√° um byte B
println(name[1..3]) // fatiar d√° uma string 'ob'
windows_newline := '\r\n' // escapar caracteres especiais como em C
assert windows_newline.len == 2
```

Em V, uma string √© um array de bytes somente leitura. Os dados da string s√£o codificados usando UTF-8. Os valores da string s√£o imut√°veis. Voc√™ n√£o pode modificar elementos:

```vlang
mut s := 'hello üåé'
s[0] = `H` // n√£o permitido
```

>erro: n√£o √© poss√≠vel atribuir a s[i], pois as strings V s√£o imut√°veis

Observe que a indexa√ß√£o de uma string produzir√° um byte, n√£o uma runa. Os √≠ndices correspondem a bytes na string, n√£o a pontos de c√≥digo Unicode. Literais de caracteres t√™m tipo runa. Para denot√°-los, use `

```·πΩlang
rocket := `üöÄ`
assert 'aloha!'[0] == `a`
```

As aspas simples e duplas podem ser usadas para denotar strings. Para consist√™ncia, vfmt converte aspas duplas em aspas simples, a menos que a string contenha um caractere de aspas simples.

Para strings brutas, prefixe r. Strings brutos n√£o s√£o escapados:

```vlang
s := r'hello\nworld'
println(s) // "hello\nworld"
```

Strings podem ser facilmente convertidos em inteiros:

```vlang
s := '42'
n := s.int() // 42
```

#### Interpola√ß√£o de string

A sintaxe de interpola√ß√£o b√°sica √© bastante simples - use ```$``` antes do nome de uma vari√°vel. A vari√°vel ser√° convertida em uma string e incorporada ao literal:

```vlang
name := 'Bob'
println('Hello, $name!') // Hello, Bob!
```

Tamb√©m funciona com os campos: **'age = $user.age'**. Se voc√™ precisar de express√µes mais complexas, use **${}**: **'can register = ${user.age > 13}'**.

Especificadores de formato semelhantes aos de **printf()** do **C** tamb√©m s√£o suportados. **f, g, x, etc**. s√£o opcionais e especificam o formato de sa√≠da. O compilador cuida do tamanho do armazenamento, portanto, n√£o h√° **hd** ou **llu**.

```vlang
x := 123.4567
println('x = ${x:4.2f}')
println('[${x:10}]') // pad com espa√ßos √† esquerda => [   123.457]
println('[${int(x):-10}]') // pad com espa√ßos √† direita => [123       ]
println('[${int(x):010}]') // preencher com zeros √† esquerda => [0000000123]
```

#### Operadores de string

```vlang
name := 'Bob'
bobby := name + 'by' // + √© usado para concatenar strings
println(bobby) // "Bobby"
mut s := 'hello '
s += 'world' // `+=` √© usado para anexar a uma string
println(s) // "hello world"
```

Todos os operadores em V devem ter valores do mesmo tipo em ambos os lados. Voc√™ n√£o pode concatenar um inteiro com uma string:

```vlang
age := 10
println('age = ' + age) // n√£o permitido
```

>erro: infix expr: n√£o √© poss√≠vel usar int (express√£o correta) como string

Temos que converter a idade em uma string:

```vlang
age := 11
println('age = ' + age.str())
```

ou use interpola√ß√£o de string (preferencial):

```vlang
age := 12
println('age = $age')
```

#### N√∫meros

```vlang
a := 123
```

Isso atribuir√° o valor de `123` a `a`. Por padr√£o, `a` ter√° o tipo `int`.

Voc√™ tamb√©m pode usar nota√ß√£o hexadecimal, bin√°ria ou octal para literais inteiros:

```vlang
a := 0x7B
b := 0b01111011
c := 0o173
```

Todos eles receber√£o o mesmo valor, 123. Todos eles ter√£o o tipo int, independentemente da nota√ß√£o usada.

V tamb√©m suporta a escrita de n√∫meros com `_` como separador:

```vlang
num := 1_000_000 // igual a 1000000
three := 0b0_11 // igual a 0b11
float_num := 3_122.55 // igual a 3122.55
hexa := 0xF_F // igual a 255
oct := 0o17_3 // igual a 0o173
```

Se quiser um tipo diferente de n√∫mero inteiro, voc√™ pode usar a convers√£o:

```vlang
a := i64(123)
b := byte(42)
c := i16(12345)
```

A atribui√ß√£o de n√∫meros de ponto flutuante funciona da mesma maneira:

```vlang
f := 1.0
f1 := f64(3.14)
f2 := f32(3.14)
```

Se voc√™ n√£o especificar o tipo explicitamente, por padr√£o, os literais flutuantes ter√£o o tipo f64.

#### Arrays

```vlang
mut nums := [1, 2, 3]
println(nums) // "[1, 2, 3]"
println(nums[1]) // "2"
nums[1] = 5
println(nums) // "[1, 5, 3]"
println(nums.len) // "3"
nums = [] // A matriz agora est√° vazia
println(nums.len) // "0"
// Declare uma matriz vazia:
users := []int{}
```

O tipo de uma matriz √© determinado pelo primeiro elemento:

- [1, 2, 3] √© uma matriz de ints ([] int)
- ['a', 'b'] √© uma matriz de strings ([] string).

O usu√°rio pode especificar explicitamente o tipo do primeiro elemento: [byte (16), 32, 64, 128]. As matrizes V s√£o homog√™neas (todos os elementos devem ter o mesmo tipo). Isso significa que c√≥digo como [1, 'a'] n√£o ser√° compilado.

O campo .len retorna o comprimento da matriz. Observe que √© um campo somente leitura e n√£o pode ser modificado pelo usu√°rio. Os campos exportados s√£o somente leitura por padr√£o em V. Consulte modificadores de acesso.

**Opera√ß√µes de array**

```vlang
mut nums := [1, 2, 3]
nums << 4
println(nums) // "[1, 2, 3, 4]"
// append array
nums << [5, 6, 7]
println(nums) // "[1, 2, 3, 4, 5, 6, 7]"
mut names := ['John']
names << 'Peter'
names << 'Sam'
// names << 10  <-- Isso n√£o vai compilar. `names` √© um array de strings.
println(names.len) // "3"
println('Alex' in names) // "false"
```

`<<` √© um operador que anexa um valor ao final do array. Ele tamb√©m pode anexar um array inteiro.

val no array retorna true se o array contiver val. Veja no operador.

**Inicializando propriedades de array**

Durante a inicializa√ß√£o, voc√™ pode especificar a capacidade do array (cap), seu comprimento inicial (len) e o elemento padr√£o (init):

```vlang
arr := []int{len: 5, init: -1}
// `[-1, -1, -1, -1, -1]`
```

Definir a capacidade melhora o desempenho das inser√ß√µes, pois reduz o n√∫mero de realoca√ß√µes necess√°rias:

```vlang
mut numbers := []int{cap: 1000}
println(numbers.len) // 0
// Agora, os elementos anexados n√£o ser√£o realocados
for i in 0 .. 1000 {
	numbers << i
}
```

Nota: O c√≥digo acima usa um intervalo para declara√ß√£o.


**M√©todos de array**

Todos os arrays podem ser facilmente impressos com `println(arr)` e convertidos em uma string com `s := arr.str()`.

A c√≥pia dos dados da matriz √© feita com `.clone()`:

```vlang
nums := [1, 2, 3]
nums_copy := nums.clone()
```

As matrizes podem ser filtradas e mapeadas com efici√™ncia com os m√©todos `.filter()` e `.map()`:

```vlang
nums := [1, 2, 3, 4, 5, 6]
even := nums.filter(it % 2 == 0)
println(even) // [2, 4, 6]
// filtro pode aceitar fun√ß√µes an√¥nimas
even_fn := nums.filter(fn (x int) bool {
	return x % 2 == 0
})
println(even_fn)
words := ['hello', 'world']
upper := words.map(it.to_upper())
println(upper) // ['HELLO', 'WORLD']
// o mapa tamb√©m pode aceitar fun√ß√µes an√¥nimas
upper_fn := words.map(fn (w string) string {
	return w.to_upper()
})
println(upper_fn) // ['HELLO', 'WORLD']
```

**it** √© uma vari√°vel embutida que se refere ao elemento atualmente sendo processado nos m√©todos de **filtro/mapa**.

Al√©m disso, **.any()** e **.all()** podem ser usados para testar convenientemente os elementos que satisfazem uma condi√ß√£o.

```vlang
nums := [1, 2, 3]
println(nums.any(it == 2)) // true
println(nums.all(it >= 2)) // false
```

**Arrays multidimensionais**

Os arrays podem ter mais de uma dimens√£o.

Exemplo de array 2D:

```vlang
mut a := [][]int{len: 2, init: []int{len: 3}}
a[0][1] = 2
println(a) // [[0, 2, 0], [0, 0, 0]]
```

Exemplo de array 3D:

```vlang
mut a := [][][]int{len: 2, init: [][]int{len: 3, init: []int{len: 2}}}
a[0][1][1] = 2
println(a) // [[[0, 0], [0, 2], [0, 0]], [[0, 0],
```

**Classificando array**

A classifica√ß√£o de matrizes de todos os tipos √© muito simples e intuitiva. As vari√°veis especiais **a** e **b** s√£o usadas ao fornecer uma condi√ß√£o de classifica√ß√£o personalizada.

```vlang
mut numbers := [1, 3, 2]
numbers.sort() // 1, 2, 3
numbers.sort(a > b) // 3, 2, 1


struct User {
	age  int
	name string
}

mut users := [User{21, 'Bob'}, User{20, 'Zarkon'}, User{25, 'Alice'}]
users.sort(a.age < b.age) // classificar pelo campo User.age int
users.sort(a.name > b.name) // classifica√ß√£o reversa pelo campo de string User.name
```

**Fatias de array**

As fatias s√£o arrays parciais. Eles representam cada elemento entre dois √≠ndices separados por um operador `..` O √≠ndice do lado direito deve ser maior ou igual ao √≠ndice do lado esquerdo.

Se um √≠ndice do lado direito estiver ausente, ser√° considerado o comprimento do array. Se um √≠ndice do lado esquerdo estiver ausente, ser√° considerado 0.

```vlang
nums := [0, 10, 20, 30, 40]
println(nums[1..4]) // [10, 20, 30]
println(nums[..4]) // [0, 10, 20, 30]
println(nums[1..]) // [10, 20, 30, 40]
```

Todas as opera√ß√µes de array podem ser realizadas em fatias. As fatias podem ser colocadas em um array do mesmo tipo.

```vlang
array_1 := [3, 5, 4, 7, 6]
mut array_2 := [0, 1]
array_2 << array_1[..3]
println(array_2) // [0, 1, 3, 5, 4]
```

#### Arrays de tamanho fixo

V tamb√©m oferece suporte a arrays com tamanho fixo. Ao contr√°rio dos arrays comuns, seu comprimento √© constante. Voc√™ n√£o pode anexar elementos a eles, nem reduzi-los. Voc√™ s√≥ pode modificar seus elementos no local.

No entanto, o acesso aos elementos de arrays de tamanho fixo √© mais eficiente, eles precisam de menos mem√≥ria do que arrays comuns e, ao contr√°rio de arrays comuns, seus dados est√£o na pilha, ent√£o voc√™ pode querer us√°-los como buffers se n√£o quiser uma pilha adicional aloca√ß√µes.

A maioria dos m√©todos √© definida para funcionar em arrays comuns, n√£o em arrays de tamanho fixo. Voc√™ pode converter uma array de tamanho fixo em um array comum com fatiamento:

```vlang
mut fnums := [3]int{} // fnums √© uma array de tamanho fixo com 3 elementos.
fnums[0] = 1
fnums[1] = 10
fnums[2] = 100
println(fnums) // => [1, 10, 100]
println(typeof(fnums).name) // => [3]int

anums := fnums[0..fnums.len]
println(anums) // => [1, 10, 100]
println(typeof(anums).name) // => []int
```

Observe que o fatiamento far√° com que os dados do array de tamanho fixo sejam copiados para o array comum rec√©m-criado.

#### Mapas

```vlang
mut m := map[string]int{} // a map with `string` keys and `int` values
m['one'] = 1
m['two'] = 2
println(m['one']) // "1"
println(m['bad_key']) // "0"
println('bad_key' in m) // Use `in` to detect whether such key exists
m.delete('two')
```

Os mapas podem ter chaves do tipo **string**, **rune**, **integer**, **float** ou **voidptr**.

Todo o mapa pode ser inicializado usando esta sintaxe curta:

```vlang
numbers := map{
	1: 'one'
	2: 'two'
}
println(numbers)
```

Se uma chave n√£o for encontrada, um valor zero √© retornado por padr√£o:

```vlang
sm := map{
	'abc': 'xyz'
}
val := sm['bad_key']
println(val) // ''
intm := map{
	1: 1234
	2: 5678
}
s := intm[3]
println(s) // 0
```

Tamb√©m √© poss√≠vel usar um bloco ou {} para lidar com as chaves ausentes:

```vlang
mm := map[string]int{}
val := mm['bad_key'] or { panic('chave n√£o encontrada') }
```

A mesma verifica√ß√£o opcional se aplica a arrays:

```vlang
arr := [1, 2, 3]
large_index := 999
val := arr[large_index] or { panic('fora dos limites') }
```

### Importa√ß√µes de m√≥dulos

Para obter informa√ß√µes sobre como criar um m√≥dulo, consulte M√≥dulos.

Os m√≥dulos podem ser importados usando **import keyword**:

```vlang
import os

fn main() {
	// read text from stdin
	name := os.input('Enter your name: ')
	println('Hello, $name!')
}
```

Este programa pode usar qualquer defini√ß√£o p√∫blica do m√≥dulo **os**, como a fun√ß√£o **input**. Consulte a documenta√ß√£o da biblioteca padr√£o para obter uma lista de m√≥dulos comuns e seus s√≠mbolos p√∫blicos.

Por padr√£o, voc√™ deve especificar o prefixo do m√≥dulo sempre que chamar uma fun√ß√£o externa. Isso pode parecer prolixo no in√≠cio, mas torna o c√≥digo muito mais leg√≠vel e f√°cil de entender - √© sempre claro qual fun√ß√£o de qual m√≥dulo est√° sendo chamado. Isso √© especialmente √∫til em grandes bases de c√≥digo.

Importa√ß√µes de m√≥dulos c√≠clicos n√£o s√£o permitidas, como em Go.

#### Importa√ß√µes seletivas

Voc√™ tamb√©m pode importar fun√ß√µes e tipos espec√≠ficos de m√≥dulos diretamente:

```vlang
import os { input }

fn main() {
	// ler texto de stdin
	name := input('Digite seu nome: ')
	println('Ol√°, $name!')
}
```

Nota: Isso n√£o √© permitido para constantes - elas devem ser sempre prefixadas.

Voc√™ pode importar v√°rios s√≠mbolos espec√≠ficos de uma vez:

```vlang
import os { input, user_os }

name := input('Digite seu nome: ')
println('Nome: $name')
os := user_os()
println('Seu sistema operacional √© ${os}.')
```

#### Aliasing de importa√ß√£o de m√≥dulo

Qualquer nome de m√≥dulo importado pode ter um **alias** usando a palavra-chave **as**:

NOTA: este exemplo n√£o ser√° compilado a menos que voc√™ tenha criado **mymod/sha256.v**

```vlang
import crypto.sha256
import mymod.sha256 as mysha256

fn main() {
	v_hash := sha256.sum('hi'.bytes()).hex()
	my_hash := mysha256.sum('hi'.bytes()).hex()
	assert my_hash == v_hash
}
```

Voc√™ n√£o pode criar um alias para uma fun√ß√£o ou tipo importado. No entanto, voc√™ pode declarar novamente um tipo.

```vlang
import time
import math

type MyTime = time.Time

fn (mut t MyTime) century() int {
	return int(1.0 + math.trunc(f64(t.year) * 0.009999794661191))
}

fn main() {
	mut my_time := MyTime{
		year: 2020
		month: 12
		day: 25
	}
	println(time.new_time(my_time).utc_string())
	println('Century: $my_time.century()')
}
```

### Declara√ß√µes e express√µes

#### if

```vlang
a := 10
b := 20
if a < b {
	println('$a < $b')
} else if a > b {
	println('$a > $b')
} else {
	println('$a == $b')
}
```

As instru√ß√µes **if** s√£o bastante diretas e semelhantes √† maioria das outras linguagens. Ao contr√°rio de outras linguagens semelhantes a C, n√£o h√° par√™nteses ao redor da condi√ß√£o e as chaves s√£o sempre necess√°rias.

**if** pode ser usado como uma express√£o:

```vlang
num := 777
s := if num % 2 == 0 { 'even' } else { 'odd' }
println(s)
// "odd"
```

**Verifica√ß√µes de tipo e convers√£o**

Voc√™ pode verificar o tipo atual de um tipo de soma usando **is** e sua forma negada **!is**.

Voc√™ pode fazer isso em um **if**:

```vlang
struct Abc {
	val string
}

struct Xyz {
	foo string
}

type Alphabet = Abc | Xyz

x := Alphabet(Abc{'test'}) // sum type
if x is Abc {
	// x √© convertido automaticamente para Abc e pode ser usado aqui println(x)
}
if x !is Abc {
	println('Not Abc')
}
```

ou usando match:

```vlang
match x {
	Abc {
		// x √© convertido automaticamente para Abc e pode ser usado aqui println(x)
	}
	Xyz {
		// x √© convertido automaticamente para Xyz e pode ser usado aqui println(x)
	}
}
```

Isso tamb√©m funciona com campos de estrutura:

```vlang
struct MyStruct {
	x int
}

struct MyStruct2 {
	y string
}

type MySumType = MyStruct | MyStruct2

struct Abc {
	bar MySumType
}

x := Abc{
	bar: MyStruct{123} // MyStruct ser√° convertido para o tipo MySumType automaticamente
}
if x.bar is MyStruct {
	// x.bar √© lan√ßado automaticamente
	println(x.bar)
}
match x.bar {
	MyStruct {
		// x.bar √© lan√ßado automaticamente
		println(x.bar)
	}
	else {}
}
```

Vari√°veis mut√°veis podem mudar e fazer uma convers√£o n√£o seria seguro. No entanto, √†s vezes √© necess√°rio ter um tipo de elenco, apesar da mutabilidade. Nesse caso, o desenvolvedor deve marcar a express√£o com uma palavra-chave **mut** para informar ao compilador que voc√™ est√° ciente do que est√° fazendo.

Funciona assim:

```vlang
mut x := MySumType(MyStruct{123})
if mut x is MyStruct {
	// x √© lan√ßado em MyStruct mesmo que seja mut√°vel
	// sem a palavra-chave mut que n√£o funcionaria println(x)
}
// mesmo com match
match mut x {
	MyStruct {
		// x √© lan√ßado em MyStruct mesmo que seja mut√°vel
		// sem a palavra-chave mut que n√£o funcionaria println(x)
	}
}
```

#### Operador in

**in** permite verificar se um **array** ou mapa cont√©m um elemento. Para fazer o oposto, use **!in**.

```vlang
nums := [1, 2, 3]
println(1 in nums) // true
println(4 !in nums) // true
m := map{
	'one': 1
	'two': 2
}
println('one' in m) // true
println('three' !in m) // true
```

Tamb√©m √© √∫til para escrever express√µes booleanas mais claras e compactas:

```vlang
enum Token {
	plus
	minus
	div
	mult
}

struct Parser {
	token Token
}

parser := Parser{}
if parser.token == .plus || parser.token == .minus || parser.token == .div || parser.token == .mult {
	// ...
}
if parser.token in [.plus, .minus, .div, .mult] {
	// ...
}
```

V otimiza tais express√µes, portanto, ambas as instru√ß√µes **if** acima produzem o mesmo c√≥digo de m√°quina e nenhum **array** √© criado.

#### Loop for

V tem apenas uma palavra-chave de loop: **for**, com v√°rias formas.

**for/in**

Esta √© a forma mais comum. Voc√™ pode us√°-lo com um **array**, **map** ou um **range** de n√∫meros.

**Array for**

```vlang
numbers := [1, 2, 3, 4, 5]
for num in numbers {
	println(num)
}
names := ['Sam', 'Peter']
for i, name in names {
	println('$i) $name')
	// Output: 0) Sam
	//         1) Peter
}
```

O formato **for value in arr** √© usado para percorrer os elementos de um array. Se um √≠ndice for necess√°rio, uma forma **for index**, **value in arr** pode ser usada.

Observe que o valor √© somente leitura. Se precisar modificar a array durante o loop, voc√™ precisar√° declarar o elemento como mut√°vel:

```vlang
mut numbers := [0, 1, 2]
for mut num in numbers {
	num++
}
println(numbers) // [1, 2, 3]
```

Quando um identificador √© apenas um √∫nico sublinhado, ele √© ignorado.

**Map for**

```vlang
m := map{
	'one': 1
	'two': 2
}
for key, value in m {
	println('$key -> $value')
	// Output: one -> 1
	//         two -> 2
}
```

A chave ou o valor podem ser ignorados usando um √∫nico sublinhado como identificador.

```vlang
m := map{
	'one': 1
	'two': 2
}
// iterate over keys
for key, _ in m {
	println(key)
	// Output: one
	//         two
}
// iterate over values
for _, value in m {
	println(value)
	// Output: 1
	//         2
}
```

**Range for**

```vlang
// Prints '01234'
for i in 0 .. 5 {
	print(i)
}
```

**low..high** significa um **range** exclusivo, que representa todos os valores de **low** at√© **up** mas n√£o incluindo **high**.

**Condi√ß√£o for**

```vlang
mut sum := 0
mut i := 0
for i <= 100 {
	sum += i
	i++
}
println(sum) // "5050"
```

Esta forma de loop √© semelhante aos loops while em outras linguagens. O loop ir√° parar de iterar assim que a condi√ß√£o booleana for avaliada como falsa. Novamente, n√£o h√° par√™nteses em torno da condi√ß√£o e as chaves s√£o sempre necess√°rias.

**Apenas for**

```vlang
mut num := 0
for {
	num += 2
	if num >= 10 {
		break
	}
}
println(num) // "10"
```

A condi√ß√£o pode ser omitida, resultando em um loop infinito.

**for estilo C**

```vlang
for i := 0; i < 10; i += 2 {
	// Don't print 6
	if i == 6 {
		continue
	}
	println(i)
}
```

Finalmente, existe o estilo **C** tradicional para **loop**. √â mais seguro do que a forma **while** porque com o √∫ltimo √© f√°cil esquecer de atualizar o contador e ficar preso em um **loop** infinito.

Aqui, **i** n√£o precisa ser declarado com **mut**, pois sempre ser√° mut√°vel por defini√ß√£o.

**Instru√ß√µes break e continue**

**break** e **continue** controlando o **loop for** mais interno por padr√£o. Voc√™ tamb√©m pode usar **break** e **continue** seguido por um nome de r√≥tulo para se referir a um **loop for** externo:

```vlang
outer: for i := 4; true; i++ {
	println(i)
	for {
		if i < 7 {
			continue outer
		} else {
			break outer
		}
	}
}
```

O r√≥tulo deve preceder imediatamente o loop externo. O c√≥digo acima √© impresso:

```shell
4
5
6
7
```

#### Match

```vlang
os := 'windows'
print('V is running on ')
match os {
	'darwin' { println('macOS.') }
	'linux' { println('Linux.') }
	else { println(os) }
}
```

Uma declara√ß√£o de correspond√™ncia √© uma maneira mais curta de escrever uma sequ√™ncia de declara√ß√µes **if - else**. Quando uma ramifica√ß√£o correspondente for encontrada, o seguinte bloco de instru√ß√£o ser√° executado. A outra ramifica√ß√£o ligada a instru√ß√£o **else** ser√° executado quando nenhum outro ramifica√ß√£o corresponder.

```vlang
number := 2
s := match number {
	1 { 'one' }
	2 { 'two' }
	else { 'many' }
}
```

Uma express√£o de correspond√™ncia retorna o valor da express√£o final da ramifica√ß√£o correspondente.

```vlang
enum Color {
	red
	blue
	green
}

fn is_red_or_blue(c Color) bool {
	return match c {
		.red, .blue { true } // a v√≠rgula pode ser usada para testar v√°rios valores values
		.green { false }
	}
}
```

Uma instru√ß√£o de correspond√™ncia tamb√©m pode ser usada para ramificar as variantes de um enum usando a sintaxe abreviada **.variant_here**. Uma ramifica√ß√£o **else** n√£o √© permitida quando todas as ramifica√ß√µes forem completas.

```vlang
c := `v`
typ := match c {
	`0`...`9` { 'digit' }
	`A`...`Z` { 'uppercase' }
	`a`...`z` { 'lowercase' }
	else { 'other' }
}
println(typ)
// 'lowercase'
```

Voc√™ tamb√©m pode usar intervalos como padr√µes de correspond√™ncia. Se o valor estiver dentro do intervalo de uma ramifica√ß√£o, essa ramifica√ß√£o ser√° executada.

Observe que os intervalos usam `...` (tr√™s pontos) em vez de `..` (dois pontos). Isso ocorre porque o intervalo inclui o √∫ltimo elemento, em vez de exclusivo (`as .. ranges`). Usar `..` em um branch de correspond√™ncia gerar√° um erro.

Nota: **match** como uma express√£o n√£o pode ser usado em instru√ß√µes loop **for** e **if**.

#### Defer

Uma instru√ß√£o **defer** adia a execu√ß√£o de um bloco de instru√ß√µes at√© que a fun√ß√£o envolvente retorne.

```vlang
import os

fn read_log() {
	mut ok := false
	mut f := os.open('log.txt') or { panic(err.msg) }
	defer {
		f.close()
	}
	// ...
	if !ok {
		// instru√ß√£o defer ser√° chamada aqui, o arquivo ser√° fechado
		return
	}
	// ...
	// instru√ß√£o defer ser√° chamada aqui, o arquivo ser√° fechado
}
```

#### Structs

```vlang
struct Point {
	x int
	y int
}

mut p := Point{
	x: 10
	y: 20
}
println(p.x) // Os campos de estrutura s√£o acessados usando um ponto
// Sintaxe literal alternativa para structs com 3 campos ou menos
p = Point{10, 20}
assert p.x == 10
```

#### Heap structs

As estruturas s√£o alocadas na pilha. Para alocar uma estrutura no **heap** e obter uma refer√™ncia a ela, use o prefixo **&**:

```vlang
struct Point {
	x int
	y int
}

p := &Point{10, 10}
// As refer√™ncias t√™m a mesma sintaxe para acessar os campos
println(p.x)
```

O tipo de **p** √© **& Point**. √â uma refer√™ncia ao **Point**. As refer√™ncias s√£o semelhantes aos ponteiros **Go** e refer√™ncias **C++**.

#### Structs embutidas

V n√£o permite subclasses, mas suporta estruturas incorporadas:

```vlang
struct Widget {
mut:
	x int
	y int
}

struct Button {
	Widget
	title string
}

mut button := Button{
	title: 'Click me'
}
button.x = 3
```

Sem a incorpora√ß√£o, ter√≠amos que nomear o campo Widget e fazer:

```vlang
button.widget.x = 3
```

#### Valores de campo padr√£o

```vlang
struct Foo {
	n   int    // n is 0 by default
	s   string // s is '' by default
	a   []int  // a is `[]int{}` by default
	pos int = -1 // custom default value
}
```

Todos os campos de estrutura s√£o zerados por padr√£o durante a cria√ß√£o da estrutura. Campos de matriz e mapa s√£o alocados.

Tamb√©m √© poss√≠vel definir valores padr√£o personalizados.

#### Os campos obrigat√≥rios

```vlang
struct Foo {
	n int [required]
}
```

Voc√™ pode marcar um campo de estrutura com o atributo [required], para dizer a V que esse campo deve ser inicializado ao criar uma inst√¢ncia dessa estrutura.

Este exemplo n√£o ser√° compilado, pois o campo n n√£o foi inicializado explicitamente:

```vlang
_ = Foo{}
```

#### Sintaxe literal curta de estrutura

```vlang
struct Point {
	x int
	y int
}

mut p := Point{
	x: 10
	y: 20
}
// you can omit the struct name when it's already known
p = {
	x: 30
	y: 4
}
assert p.y == 4
```

Omitir o nome da estrutura tamb√©m funciona para retornar um literal de estrutura ou passar um como um argumento de fun√ß√£o.

**Argumentos literais de estrutura final**

V n√£o tem argumentos de fun√ß√£o padr√£o ou argumentos nomeados, pois essa sintaxe literal de estrutura final pode ser usada em seu lugar:

```vlang
struct ButtonConfig {
	text        string
	is_disabled bool
	width       int = 70
	height      int = 20
}

struct Button {
	text   string
	width  int
	height int
}

fn new_button(c ButtonConfig) &Button {
	return &Button{
		width: c.width
		height: c.height
		text: c.text
	}
}

button := new_button(text: 'Click me', width: 100)
// a altura n√£o est√° definida, ent√£o √© o valor padr√£o
assert button.height == 20
```

Como voc√™ pode ver, o nome da estrutura e as chaves podem ser omitidos, em vez de:

```vlang
new_button(ButtonConfig{text:'Click me', width:100})
```

Isso s√≥ funciona para fun√ß√µes que usam uma estrutura para o √∫ltimo argumento.

#### Modificadores de acesso

Os campos **Struct** s√£o privados e imut√°veis por padr√£o (tornando os **structs** imut√°veis tamb√©m). Seus modificadores de acesso podem ser alterados com **pub** e **mut**. No total, existem 5 op√ß√µes poss√≠veis:

```vlang
struct Foo {
	a int // privado imut√°vel (padr√£o)
mut:
	b int // private mutable
	c int // (voc√™ pode listar v√°rios campos com o mesmo modificador de acesso)
pub:
	d int // public imut√°vel (somente leitura)
pub mut:
	e int // p√∫blico, mas mut√°vel apenas no m√≥dulo pai
__global:
	// (n√£o recomendado para uso, √© por isso que a palavra-chave 'global' come√ßa com __)
	f int // p√∫blico e mut√°vel dentro e fora do m√≥dulo pai
}
```

Por exemplo, aqui est√° o tipo de string definido no m√≥dulo integrado:

```vlang
struct string {
	str byteptr
pub:
	len int
}
```

√â f√°cil ver a partir dessa defini√ß√£o que string √© um tipo imut√°vel. O ponteiro de **byte** com os dados da **string** n√£o pode ser acessado externamente. O campo **len** √© p√∫blico, mas imut√°vel:

```vlang
fn main() {
	str := 'hello'
	len := str.len // OK
	str.len++      // Erro de compila√ß√£o
}
```

Isso significa que definir campos p√∫blicos somente leitura √© muito f√°cil em V, sem necessidade de **getters/setters** ou propriedades.

#### M√©todos

```vlang
struct User {
	age int
}

fn (u User) can_register() bool {
	return u.age > 16
}

user := User{
	age: 10
}
println(user.can_register()) // "false"
user2 := User{
	age: 20
}
println(user2.can_register()) // "true"
```

V n√£o tem classes, mas voc√™ pode definir m√©todos em tipos. Um m√©todo √© uma fun√ß√£o com um argumento receptor especial. O receptor aparece em sua pr√≥pria lista de argumentos entre a palavra-chave fn e o nome do m√©todo. Os m√©todos devem estar no mesmo m√≥dulo que o tipo de receptor.

Neste exemplo, o m√©todo **can_register** possui um receptor do tipo **user** denominado **u**. A conven√ß√£o n√£o √© usar nomes de destinat√°rios como **self** ou **this**, mas um nome curto, de prefer√™ncia com uma letra.

#### Unions

Assim como **structs**, os **unions** suportam a incorpora√ß√£o.

```vlang
struct Rgba32_Component {
	r byte
	g byte
	b byte
	a byte
}

union Rgba32 {
	Rgba32_Component
	value u32
}

clr1 := Rgba32{
	value: 0x008811FF
}

clr2 := Rgba32{
	Rgba32_Component: {
		a: 128
	}
}

sz := sizeof(Rgba32)
unsafe {
	println('Size: ${sz}B,clr1.b: $clr1.b,clr2.b: $clr2.b')
}
```

Sa√≠da: Tamanho: 4B, clr1.b: 136, clr2.b: 0

O acesso de membro da union deve ser executado em um bloco n√£o seguro.

Observe que os argumentos de estrutura incorporados n√£o s√£o necessariamente armazenados na ordem listada.

### Fun√ß√µes 2

#### Fun√ß√µes puras por padr√£o

As fun√ß√µes V s√£o puras por padr√£o, o que significa que seus valores de retorno s√£o uma fun√ß√£o de seus argumentos apenas e sua avalia√ß√£o n√£o tem efeitos colaterais (al√©m de **E/S**).

Isso √© obtido por falta de vari√°veis globais e todos os argumentos de fun√ß√£o sendo imut√°veis por padr√£o, mesmo quando as refer√™ncias s√£o passadas.

V n√£o √© uma linguagem puramente funcional.

Existe um sinalizador do compilador para habilitar vari√°veis ‚Äã‚Äãglobais (--enable-globals), mas isso se destina a aplicativos de baixo n√≠vel como kernels e drivers.

**Argumentos mut√°veis**

√â poss√≠vel modificar os argumentos da fun√ß√£o usando a palavra-chave **mut**:

```vlang
struct User {
	name string
mut:
	is_registered bool
}

fn (mut u User) register() {
	u.is_registered = true
}

mut user := User{}
println(user.is_registered) // "false"
user.register()
println(user.is_registered) // "true"
```

Neste exemplo, o receptor (que √© simplesmente o primeiro argumento) √© marcado como mut√°vel, portanto, **register()** pode alterar o objeto do usu√°rio. O mesmo funciona com argumentos de n√£o receptores:

```vlang
fn multiply_by_2(mut arr []int) {
	for i in 0 .. arr.len {
		arr[i] *= 2
	}
}

mut nums := [1, 2, 3]
multiply_by_2(mut nums)
println(nums)
// "[2, 4, 6]"
```

Observe que voc√™ deve adicionar **mut** antes de **nums** ao chamar esta fun√ß√£o. Isso deixa claro que a fun√ß√£o que est√° sendo chamada modificar√° o valor.

√â prefer√≠vel retornar valores em vez de modificar argumentos. A modifica√ß√£o de argumentos deve ser feita apenas em partes cr√≠ticas de desempenho de seu aplicativo para reduzir as aloca√ß√µes e c√≥pias.

Por esta raz√£o, V n√£o permite a modifica√ß√£o de argumentos com tipos primitivos (por exemplo, inteiros). Apenas tipos mais complexos, como matrizes e mapas, podem ser modificados.

Use **user.register()** ou **user = register(user)** em vez de register (**mut user**).

**Sintaxe de atualiza√ß√£o de Struct**

V torna mais f√°cil retornar uma vers√£o modificada de um objeto:

```vlang
struct User {
	name          string
	age           int
	is_registered bool
}

fn register(u User) User {
	return {
		...u
		is_registered: true
	}
}

mut user := User{
	name: 'abc'
	age: 23
}
user = register(user)
println(user)
```
#### N√∫mero vari√°vel de argumentos

```vlang
fn sum(a ...int) int {
	mut total := 0
	for x in a {
		total += x
	}
	return total
}

println(sum()) // 0
println(sum(1)) // 1
println(sum(2, 3)) // 5
// using array decomposition
a := [2, 3, 4]
println(sum(...a)) // <-- using prefix ... here. output: 9
b := [5, 6, 7]
println(sum(...b)) // output: 18
```

#### Fun√ß√µes an√¥nimas e de alta ordem

```vlang
fn sqr(n int) int {
	return n * n
}

fn cube(n int) int {
	return n * n * n
}

fn run(value int, op fn (int) int) int {
	return op(value)
}

fn main() {
	// Functions can be passed to other functions
	println(run(5, sqr)) // "25"
	// Anonymous functions can be declared inside other functions:
	double_fn := fn (n int) int {
		return n + n
	}
	println(run(5, double_fn)) // "10"
	// Functions can be passed around without assigning them to variables:
	res := run(5, fn (n int) int {
		return n + n
	})
	println(res) // "10"
	// You can even have an array/map of functions:
	fns := [sqr, cube]
	println(fns[0](10)) // "100"
	fns_map := map{
		'sqr':  sqr
		'cube': cube
	}
	println(fns_map['cube'](2)) // "8"
}
```

#### Refer√™ncias

```vlang
struct Foo {}

fn (foo Foo) bar_method() {
	// ...
}

fn bar_function(foo Foo) {
	// ...
}
```

Se um argumento de fun√ß√£o for imut√°vel (como **foo** nos exemplos acima), V pode pass√°-lo por valor ou por refer√™ncia. O compilador decidir√° e o desenvolvedor n√£o precisa pensar sobre isso.

Voc√™ n√£o precisa mais se lembrar se deve passar a estrutura por valor ou por refer√™ncia.

Voc√™ pode garantir que a estrutura seja sempre passada por refer√™ncia adicionando **&**:

```vlang
struct Foo {
	abc int
}

fn (foo &Foo) bar() {
	println(foo.abc)
}
```

**foo** ainda √© imut√°vel e n√£o pode ser alterado. Para isso, (**mut foo Foo**) deve ser usado.

Em geral, as refer√™ncias de V s√£o semelhantes aos ponteiros **Go** e refer√™ncias **C++**. Por exemplo, uma defini√ß√£o de estrutura de √°rvore gen√©rica seria assim:

```vlang
struct Node<T> {
	val   T
	left  &Node
	right &Node
}
```

#### Constantes

```vlang
const (
	pi    = 3.14
	world = '‰∏ñÁïå'
)

println(pi)
println(world)
```

As constantes s√£o declaradas com **const**. Eles s√≥ podem ser definidos no n√≠vel do m√≥dulo (fora das fun√ß√µes). Os valores constantes nunca podem ser alterados. Voc√™ tamb√©m pode declarar uma √∫nica constante separadamente:

```vlang
const e = 2.71828
```

As constantes V s√£o mais flex√≠veis do que na maioria dos idiomas. Voc√™ pode atribuir valores mais complexos:

```vlang
struct Color {
	r int
	g int
	b int
}

fn rgb(r int, g int, b int) Color {
	return Color{
		r: r
		g: g
		b: b
	}
}

const (
	numbers = [1, 2, 3]
	red     = Color{
		r: 255
		g: 0
		b: 0
	}
	// avaliar chamada de fun√ß√£o em tempo de compila√ß√£o*
	blue    = rgb(0, 0, 255)
)

println(numbers)
println(red)
println(blue)
```

**\*WIP** - por enquanto, as chamadas de fun√ß√£o s√£o avaliadas na inicializa√ß√£o do programa

Vari√°veis globais normalmente n√£o s√£o permitidas, ent√£o isso pode ser muito √∫til.

**Prefixo de m√≥dulo necess√°rio**

Ao nomear constantes, **snake_case** deve ser usado. Para distinguir consts de vari√°veis locais, o caminho completo para consts deve ser especificado. Por exemplo, para acessar o **PI const**, o nome **math.pi** completo deve ser usado tanto fora do m√≥dulo **math** quanto dentro dele. Essa restri√ß√£o √© relaxada apenas para o m√≥dulo **main** (aquele que cont√©m seu **fn main()**), onde voc√™ pode usar o nome n√£o qualificado de constantes definidas l√°, ou seja, n√∫meros, em vez de **main.numbers**.

**vfmt** cuida desta regra, ent√£o voc√™ pode digitar **println(pi**) dentro do m√≥dulo math, e **vfmt** ir√° atualiz√°-lo automaticamente para println (**math.pi**).

#### Fun√ß√µes integradas

Algumas fun√ß√µes s√£o integradas como **println**. Aqui est√° a lista completa:

```vlang
fn print(s string) // imprima qualquer coisa no sdtout
fn println(s string) // imprime qualquer coisa e uma nova linha no sdtout

fn eprint(s string) // mesmo que print(), mas use stderr
fn eprintln(s string) // mesmo que println(), mas use stderr

fn exit(code int) // encerre o programa com um c√≥digo de erro personalizado
fn panic(s string) // imprime uma mensagem e retrocede em stderr, e termina o programa com o c√≥digo de erro 1
fn print_backtrace() // imprimir backtraces em stderr
```

**println** √© uma fun√ß√£o embutida simples, mas poderosa, que pode imprimir qualquer coisa: **strings**, **n√∫meros**, **arrays**, **maps**, **structs**.

```vlang
struct User {
	name string
	age  int
}

println(1) // "1"
println('hi') // "hi"
println([1, 2, 3]) // "[1, 2, 3]"
println(User{ name: 'Bob', age: 20 }) // "User{name:'Bob', age:20}"
```

#### Impress√£o de tipos personalizados

Se voc√™ deseja definir um valor de impress√£o personalizado para o seu tipo, basta definir um m√©todo de **string .str()**:

```vlang
struct Color {
	r int
	g int
	b int
}

pub fn (c Color) str() string {
	return '{$c.r, $c.g, $c.b}'
}

red := Color{
	r: 255
	g: 0
	b: 0
}
println(red)
```

#### M√≥dulos

Cada arquivo na raiz de uma pasta faz parte do mesmo m√≥dulo. Programas simples n√£o precisam especificar o nome do m√≥dulo; nesse caso, o padr√£o √© **'main'**.

V √© uma linguagem muito modular. A cria√ß√£o de m√≥dulos reutiliz√°veis √© incentivada e muito f√°cil de fazer. Para criar um novo m√≥dulo, crie um diret√≥rio com o nome do seu m√≥dulo contendo arquivos .v com o c√≥digo:

```shell
cd ~/code/modules
mkdir mymodule
vim mymodule/myfile.v
```

```vlang
// myfile.v
module mymodule

// Para exportar uma fun√ß√£o, temos que usar `pub`
pub fn say_hi() {
	println('ol√° do meu m√≥dulo!')
}
```

Agora voc√™ pode usar **mymodule** em seu c√≥digo:

```vlang
import mymodule

fn main() {
	mymodule.say_hi()
}
```

- Os nomes dos m√≥dulos devem ser curtos, com menos de 10 caracteres.
- Os nomes dos m√≥dulos devem usar snake_case.
- Importa√ß√µes circulares n√£o s√£o permitidas.
- Voc√™ pode ter quantos arquivos .v desejar em um m√≥dulo.
- Voc√™ pode criar m√≥dulos em qualquer lugar.
- Todos os m√≥dulos s√£o compilados estaticamente em um √∫nico execut√°vel.

**fun√ß√µes init**

Se voc√™ quiser que um m√≥dulo chame automaticamente algum c√≥digo de **configura√ß√£o/inicializa√ß√£o** quando for importado, voc√™ pode usar uma fun√ß√£o **init** do m√≥dulo:

```vlang
fn init() {
	// your setup code here ...
}
```

A fun√ß√£o **init** n√£o pode ser p√∫blica - ela ser√° chamada automaticamente. Este recurso √© particularmente √∫til para inicializar uma biblioteca C.

#### Tipos 2

**Interfaces**

```vlang
struct Dog {
	breed string
}

struct Cat {
	breed string
}

fn (d Dog) speak() string {
	return 'woof'
}

fn (c Cat) speak() string {
	return 'meow'
}

// ao contr√°rio de Go e TypeScript, as interfaces de V podem definir campos, n√£o apenas m√©todos.
interface Speaker {
	breed string
	speak() string
}

dog := Dog{'Leonberger'}
cat := Cat{'Siamese'}

mut arr := []Speaker{}
arr << dog
arr << cat
for item in arr {
	println('a $item.breed says: $item.speak()')
}
```

Um tipo implementa uma interface implementando seus m√©todos e campos. N√£o h√° declara√ß√£o expl√≠cita de inten√ß√£o, nem palavra-chave "**implements**".

**Casting de uma interface**

Podemos testar o tipo subjacente de uma interface usando operadores de convers√£o din√¢mica:

```vlang
interface Something {}

fn announce(s Something) {
	if s is Dog {
		println('a $s.breed dog') // `s` is automatically cast to `Dog` (smart cast)
	} else if s is Cat {
		println('a $s.breed cat')
	} else {
		println('something else')
	}
}
```

Para obter mais informa√ß√µes, consulte [Casts din√¢micos](https://github.com/vlang/v/blob/master/doc/docs.md#dynamic-casts).

**Defini√ß√µes de m√©todos de interface**

Tamb√©m ao contr√°rio do **Go**, uma interface pode implementar um m√©todo. Esses m√©todos n√£o s√£o implementados por **structs** que implementam essa interface.

Quando uma estrutura √© encapsulada em uma interface que implementou um m√©todo com o mesmo nome daquele implementado por esta estrutura, apenas o m√©todo implementado na interface √© chamado.

```vlang
struct Cat {}

fn (c Cat) speak() string {
	return 'meow!'
}

interface Adoptable {}

fn (a Adoptable) speak() string {
	return 'adote-me!'
}

fn new_adoptable() Adoptable {
	return Cat{}
}

fn main() {
	cat := Cat{}
	assert cat.speak() == 'Miau!'
	a := new_adoptable()
	assert a.speak() == 'adote-me!'
	if a is Cat {
		println(a.speak()) // Miau!
	}
}
```

#### Enums

```vlang
enum Color {
	red
	green
	blue
}

mut color := Color.red
// V sabe que `color` √© uma` Color`. N√£o h√° necessidade de usar `color = Color.green` aqui.
color = .green
println(color) // "verde"
match color {
	.red { println('a cor era vermelha') }
	.green { println('a cor era verde') }
	.blue { println('a cor era azul') }
}
```

A correspond√™ncia de **enum** deve ser exaustiva ou ter uma ramifica√ß√£o **else**. Isso garante que, se um novo campo enum for adicionado, ele ser√° tratado em todos os lugares do c√≥digo.

Os campos **enum** n√£o podem reutilizar palavras-chave reservadas. No entanto, palavras-chave reservadas podem ser escapadas com um **@**.

```vlang
enum Color {
	@none
	red
	green
	blue
}

color := Color.@none
println(color)
```

Inteiros podem ser atribu√≠dos a campos **enum**.

```vlang
enum Mercado {
	ma√ß√£
	laranja = 5
	pera
}

g1 := int(Mercado.ma√ß√£)
g2 := int(Mercado.laranja)
g3 := int(Mercado.pera)
println('IDs de mercado: $g1, $g2, $g3')
```

Resultado: IDs de mercado: 0, 5, 6.

Opera√ß√µes n√£o s√£o permitidas em vari√°veis **enum**; eles devem ser explicitamente convertidos em **int**.

**Tipos de soma**

Uma inst√¢ncia de tipo de soma pode conter um valor de v√°rios tipos diferentes. Use a palavra-chave type para declarar um tipo de soma:

```vlang
struct Moon {}

struct Mars {}

struct Venus {}

type World = Mars | Moon | Venus

sum := World(Moon{})
assert sum.type_name() == 'Moon'
println(sum)
```

O m√©todo interno **type_name** retorna o nome do tipo mantido atualmente.

Com os tipos de soma, voc√™ pode construir estruturas recursivas e escrever um c√≥digo conciso, mas poderoso, nelas.

```vlang
// V's binary tree
struct Empty {}

struct Node {
	value f64
	left  Tree
	right Tree
}

type Tree = Empty | Node

// sum up all node values
fn sum(tree Tree) f64 {
	return match tree {
		Empty { f64(0) } // TODO: as match gets smarter just remove f64()
		Node { tree.value + sum(tree.left) + sum(tree.right) }
	}
}

fn main() {
	left := Node{0.2, Empty{}, Empty{}}
	right := Node{0.3, Empty{}, Node{0.4, Empty{}, Empty{}}}
	tree := Node{0.5, left, right}
	println(sum(tree)) // 0.2 + 0.3 + 0.4 + 0.5 = 1.4
}
```

**Casts din√¢micos**

Para verificar se uma inst√¢ncia de tipo de soma cont√©m um determinado tipo, use soma √© Tipo. Para lan√ßar um tipo de soma para uma de suas variantes, voc√™ pode usar soma como Tipo:

```vlang
struct Moon {}

struct Mars {}

struct Venus {}

type World = Mars | Moon | Venus

fn (m Mars) dust_storm() bool {
	return true
}

fn main() {
	mut w := World(Moon{})
	assert w is Moon
	w = Mars{}
	// use `as` to access the Mars instance
	mars := w as Mars
	if mars.dust_storm() {
		println('bad weather!')
	}
}
```

assim como entrar√° em p√¢nico se **w** n√£o contiver uma inst√¢ncia de **Mars**. Uma maneira mais segura √© usar uma convers√£o inteligente.

**Convers√£o inteligente**

```vlang
if w is Mars {
	assert typeof(w).name == 'Mars'
	if w.dust_storm() {
		println('bad weather!')
	}
}
```

**w** tem o tipo **Mars** dentro do corpo da instru√ß√£o **if**. Isso √© conhecido como tipagem sens√≠vel ao fluxo. Se **w** for um identificador mut√°vel, n√£o seria seguro se o compilador o convertesse de maneira inteligente sem um aviso. √â por isso que voc√™ deve declarar um **mut** antes da express√£o **is**:

```vlang
if mut w is Mars {
	assert typeof(w).name == 'Mars'
	if w.dust_storm() {
		println('mau tempo!')
	}
}
```

Caso contr√°rio, **w** manteria seu tipo original.

>Isso funciona para vari√°veis simples e express√µes complexas como **user.name**

**Tipos de soma correspondentes**

Voc√™ tamb√©m pode usar a correspond√™ncia para determinar a variante:

```vlang
struct Moon {}

struct Mars {}

struct Venus {}

type World = Mars | Moon | Venus

fn open_parachutes(n int) {
	println(n)
}

fn land(w World) {
	match w {
		Moon {} // sem atmosfera
		Mars {
			// atmosfera leve
			open_parachutes(3)
		}
		Venus {
			// atmosfera pesada
			open_parachutes(1)
		}
	}
}
```

**match** deve ter um padr√£o para cada variante ou ter um outro **branch**.

```vlang
struct Moon {}
struct Mars {}
struct Venus {}

type World = Moon | Mars | Venus

fn (m Moon) moon_walk() {}
fn (m Mars) shiver() {}
fn (v Venus) sweat() {}

fn pass_time(w World) {
	match w {
		// usando a vari√°vel de correspond√™ncia secund√°ria, neste caso `w` (convers√£o inteligente)
		Moon { w.moon_walk() }
		Mars { w.shiver() }
		else {}
	}
}
```

**Tipos option/result e tratamento de erros**

Os tipos de **option** s√£o declarados com **?type**:

```vlang
struct User {
	id   int
	name string
}

struct Repo {
	users []User
}

fn (r Repo) find_user_by_id(id int) ?User {
	for user in r.users {
		if user.id == id {
			// V envolve isso automaticamente em um tipo option
			return user
		}
	}
	return error('User $id not found')
}

fn main() {
	repo := Repo{
		users: [User{1, 'Andrew'}, User{2, 'Bob'}, User{10, 'Charles'}]
	}
	user := repo.find_user_by_id(10) or { // Os tipos de option devem ser tratados como blocos `or`
		return
	}
	println(user.id) // "10"
	println(user.name) // "Charles"
}
```

V combina option e result em um tipo, ent√£o voc√™ n√£o precisa decidir qual usar.

A quantidade de trabalho necess√°ria para "atualizar" uma fun√ß√£o para uma fun√ß√£o opcional √© m√≠nima; voc√™ tem que adicionar um **?** para o tipo de retorno e retorna um erro quando algo d√° errado.

Se voc√™ n√£o precisa retornar uma mensagem de erro, voc√™ pode simplesmente **return none** (este √© um equivalente mais eficiente do **return error("")**).

Este √© o mecanismo prim√°rio para tratamento de erros em V. Eles ainda s√£o valores, como em **Go**, mas a vantagem √© que os erros n√£o podem ser solucionados e trat√°-los √© muito menos prolixo. Ao contr√°rio de outras linguagens, V n√£o lida com exce√ß√µes com blocos **throw/try/catch**.

**err** √© definido dentro de um bloco **or** e √© definido como a mensagem de **string** passada para a fun√ß√£o **error()**. **err** estar√° vazio se **none** for retornado.

```vlang
user := repo.find_user_by_id(7) or {
	println(err) // "User 7 not found"
	return
}
```

#### Manipula√ß√£o de opcionais

Existem quatro maneiras de lidar com um opcional. O primeiro m√©todo √© propagar o erro:

```vlang
import net.http

fn f(url string) ?string {
	resp := http.get(url) ?
	return resp.text
}
```

**http.get** retorna **?http.Response**. Porque **?** segue a chamada, o erro ser√° propagado para o chamador de **f**. Ao usar **?** ap√≥s uma chamada de fun√ß√£o que produz um opcional, a fun√ß√£o envolvente tamb√©m deve retornar um opcional. Se a propaga√ß√£o do erro for usada na fun√ß√£o **main()**, ela entrar√° em **panic**, uma vez que o erro n√£o pode ser propagado mais.

O corpo de **f** √© essencialmente uma vers√£o condensada de:

```vlang
	resp := http.get(url) or { return err }
	return resp.text
```

O segundo m√©todo √© interromper a execu√ß√£o mais cedo:

```vlang
user := repo.find_user_by_id(7) or { return }
```

Aqui, voc√™ pode chamar **panic()** ou **exit()**, que interromper√° a execu√ß√£o de todo o programa, ou usar uma instru√ß√£o de fluxo de controle (retornar, interromper, continuar, etc) para interromper o bloco atual. Observe que **break** e **continue** s√≥ podem ser usados dentro de um **loop for**.

O V n√£o tem uma maneira de "**unwrap**" √† for√ßa um opcional (como outras linguagens fazem, por exemplo, Rust's **unwrap()** ou Swift's!). Para fazer isso, use ou {panic (err.msg)}.

O terceiro m√©todo √© fornecer um valor padr√£o no final do bloco **or**. Em caso de erro, esse valor seria atribu√≠do em seu lugar, portanto, deve ser do mesmo tipo que o conte√∫do do tratamento de **option**.

```vlang
fn do_something(s string) ?string {
	if s == 'foo' {
		return 'foo'
	}
	return error('invalid string') // Could be `return none` as well
}

a := do_something('foo') or { 'default' } // a will be 'foo'
b := do_something('bar') or { 'default' } // b will be 'default'
println(a)
println(b)
```

O quarto m√©todo deve ser usado para desembrulhar:

```vlang
import net.http

if resp := http.get('https://google.com') {
	println(resp.text) // resp is a http.Response, not an optional
} else {
	println(err)
}
```

Acima, **http.get** retorna um **?http.Response**. **resp** est√° apenas no escopo para o primeiro ramo **if**. **err** est√° apenas no escopo do ramo **else**.

#### Gen√©ricos

```vlang
struct Repo<T> {
	db DB
}

struct User {
	id   int
	name string
}

struct Post {
	id   int
	user_id int
	title string
	body string
}

fn new_repo<T>(db DB) Repo<T> {
	return Repo<T>{db: db}
}

// Esta √© uma fun√ß√£o gen√©rica. V ir√° ger√°-lo para cada tipo com o qual √© usado.
fn (r Repo<T>) find_by_id(id int) ?T {
	table_name := T.name // neste exemplo, obter o nome do tipo nos d√° o nome da tabela
	return r.db.query_one<T>('select * from $table_name where id = ?', id)
}

db := new_db()
users_repo := new_repo<User>(db) // retorna Repo<User>
posts_repo := new_repo<Post>(db) // retorna Repo<Post>
user := users_repo.find_by_id(1)? // find_by_id<User>
post := posts_repo.find_by_id(1)? // find_by_id<Post>
```

Atualmente, as defini√ß√µes de fun√ß√£o gen√©rica devem declarar seus par√¢metros de tipo, mas no futuro V inferir√° par√¢metros de tipo gen√©rico a partir de nomes de tipo de letra √∫nica em tipos de par√¢metro de tempo de execu√ß√£o. √â por isso que **find_by_id** pode omitir `<T>`, porque o argumento do receptor **r** usa um tipo gen√©rico **T**.

Outro exemplo:

```vlang
fn compare<T>(a T, b T) int {
	if a < b {
		return -1
	}
	if a > b {
		return 1
	}
	return 0
}

// compare<int>
println(compare(1, 0)) // Outputs: 1
println(compare(1, 1)) //          0
println(compare(1, 2)) //         -1
// compare<string>
println(compare('1', '0')) // Outputs: 1
println(compare('1', '1')) //          0
println(compare('1', '2')) //         -1
// compare<f64>
println(compare(1.1, 1.0)) // Outputs: 1
println(compare(1.1, 1.1)) //          0
println(compare(1.1, 1.2)) //         -1
```

#### Simultaneidade

#### Gerando Tarefas Simult√¢neas

O modelo de simultaneidade de **V** √© muito semelhante ao de **Go**. Para executar **foo()** simultaneamente em uma **thread** diferente, basta cham√°-lo com go **foo()**:

```vlang
import math

fn p(a f64, b f64) { // fun√ß√£o comum sem valor de retorno
	c := math.sqrt(a * a + b * b)
	println(c)
}

fn main() {
	go p(3, 4)
	// p ser√° executado em thread paralela
}
```

√Äs vezes, √© necess√°rio esperar at√© que uma **thread** paralela termine. Isso pode ser feito atribuindo um identificador ao **thread** iniciado e chamando o m√©todo **wait()** para este identificador mais tarde:

```vlang
import math

fn p(a f64, b f64) { // ordinary function without return value
	c := math.sqrt(a * a + b * b)
	println(c) // prints `5`
}

fn main() {
	h := go p(3, 4)
	// p() runs in parallel thread
	h.wait()
	// p() has definitely finished
}
```

Essa abordagem tamb√©m pode ser usada para obter um valor de retorno de uma fun√ß√£o que √© executada em um **thread** paralelo. N√£o h√° necessidade de modificar a pr√≥pria fun√ß√£o para poder cham√°-la simultaneamente.

```vlang
import math { sqrt }

fn get_hypot(a f64, b f64) f64 { //       ordinary function returning a value
	c := sqrt(a * a + b * b)
	return c
}

fn main() {
	g := go get_hypot(54.06, 2.08) // gerar thread e lidar com ela
	h1 := get_hypot(2.32, 16.74) //   fa√ßa algum outro c√°lculo aqui
	h2 := g.wait() //                 obter o resultado da thread gerada
	println('Results: $h1, $h2') //   imprime `Results: 16,9, 54,1`
}
```

Se houver um grande n√∫mero de tarefas, pode ser mais f√°cil gerenci√°-las usando uma variedade de **threads**.

```vlang
import time

fn task(id int, duration int) {
	println('task $id begin')
	time.sleep(duration * time.millisecond)
	println('task $id end')
}

fn main() {
	mut threads := []thread{}
	threads << go task(1, 500)
	threads << go task(2, 900)
	threads << go task(3, 100)
	threads.wait()
	println('done')
}

// Output:
// task 1 begin
// task 2 begin
// task 3 begin
// task 3 end
// task 1 end
// task 2 end
// done
```

Al√©m disso, para **threads** que retornam o mesmo tipo, chamar **wait()** no **array** de **threads** retornar√° todos os valores calculados.

```vlang
fn expensive_computing(i int) int {
	return i * i
}

fn main() {
	mut threads := []thread int{}
	for i in 1 .. 10 {
		threads << go expensive_computing(i)
	}
	// Join all tasks
	r := threads.wait()
	println('Todos os trabalhos conclu√≠dos: $r')
}

// Resultado: Todos os trabalhos conclu√≠dos: [1, 4, 9, 16, 25, 36, 49, 64, 81]
```

#### Canais

Os canais s√£o a forma preferida de comunica√ß√£o entre as **co-rotinas**. Os canais da V funcionam basicamente como os do **Go**. Voc√™ pode empurrar objetos para um canal em uma extremidade e eliminar objetos na outra extremidade. Os canais podem ser armazenados em **buffer** ou sem **buffer** e √© poss√≠vel selecionar (**select**) v√°rios canais.

**Sintaxe e uso**

Os canais t√™m o tipo **chan objtype**. Um comprimento de **buffer** opcional pode ser especificado como a propriedade **cap** na declara√ß√£o:

```vlagn
ch := chan int{} // unbuffered - "synchronous"
ch2 := chan f64{cap: 100} // buffer length 100
```

Os canais n√£o precisam ser declarados como **mut**. O comprimento do **buffer** n√£o √© parte do tipo, mas uma propriedade do objeto de canal individual. Os canais podem ser passados para co-rotinas como vari√°veis normais:

```vlang
fn f(ch chan int) {
	// ...
}

fn main() {
	ch := chan int{}
	go f(ch)
	// ...
}
```

Os objetos podem ser enviados para os canais usando o operador de seta(arrow). O mesmo operador pode ser usado para eliminar objetos da outra extremidade:

```vlang
ch := chan int{}
ch2 := chan f64{}
n := 5
x := 7.3
ch <- n
// push
ch2 <- x
mut y := f64(0.0)
m := <-ch // pop creating new variable
y = <-ch2 // pop into existing variable
```

Um canal pode ser fechado para indicar que nenhum outro objeto pode ser empurrado. Qualquer tentativa de fazer isso resultar√° em um **panic** de tempo de execu√ß√£o (com exce√ß√£o de **select** e **try_push()** - veja abaixo). As tentativas de pop retornar√£o imediatamente se o canal associado tiver sido fechado e o buffer estiver vazio. Esta situa√ß√£o pode ser tratada usando uma ramifica√ß√£o ou (consulte [Tratamento de opcionais](https://github.com/vlang/v/blob/master/doc/docs.md#handling-optionals)).

```vlang
ch := chan int{}
ch2 := chan f64{}
// ...
ch.close()
// ...
m := <-ch or {
    println('channel has been closed')
}

// propagate error
y := <-ch2 ?
```

**Sele√ß√£o de canal**

O comando **select** permite monitorar v√°rios canais ao mesmo tempo sem carga da **CPU** percept√≠vel. Consiste em uma lista de poss√≠veis transfer√™ncias e ramos associados de instru√ß√µes - semelhante ao comando **match**:

```vlang
import time
fn main () {
	c := chan f64{}
	ch := chan f64{}
	ch2 := chan f64{}
	ch3 := chan f64{}
	mut b := 0.0
	// ...
	select {
		a := <-ch {
			// fa√ßa algo com `a`
		}
		b = <-ch2 {
			// fazer algo com a vari√°vel pr√©-declarada `b`
		}
		ch3 <- c {
			// fa√ßa algo se `c` foi enviado
		}
		> 500 * time.millisecond {
			// fa√ßa algo se nenhum canal estiver pronto em 0,5s
		}
	}
}
```

O **branch** de tempo limite √© opcional. Se estiver ausente, **select** espera por um per√≠odo de tempo ilimitado. Tamb√©m √© poss√≠vel prosseguir imediatamente se nenhum canal estiver pronto no momento em que o **select** √© chamado adicionando um outro **else { ... } branch** . **else** e **> timeout** s√£o mutuamente exclusivos.

O comando **select** pode ser usado como uma express√£o do tipo **bool** que se torna **false** se todos os canais forem fechados:

```vlang
if select {
	ch <- a {
		// ...
	}
} {
// channel was open
} else {
	// channel is closed
}
```

**Recursos especiais do canal**

Para fins especiais, existem algumas propriedades e m√©todos integrados:

```vlang
struct Abc {
	x int
}

a := 2.13
ch := chan f64{}
res := ch.try_push(a) // tente executar `ch <- a`
println(res)
l := ch.len // n√∫mero de elementos na fila
c := ch.cap // comprimento m√°ximo da fila
is_closed := ch.closed // sinalizador bool - `ch` foi fechado
println(l)
println(c)
mut b := Abc{}
ch2 := chan Abc{}
res2 := ch2.try_pop(b) // tente executar `b = <-ch2`
```

Os m√©todos **try_push/pop()** retornar√£o imediatamente com um dos resultados **.success**, **.not_ready** ou **.closed** - dependendo se o objeto foi transferido ou o motivo pelo qual n√£o. O uso desses m√©todos e propriedades na produ√ß√£o n√£o √© recomendado - algoritmos baseados neles geralmente est√£o sujeitos a condi√ß√µes de corrida. Principalmente **.len** e **.closed** n√£o devem ser usados para tomar decis√µes. Use ou ramifica√ß√µes, propaga√ß√£o de erro ou selecione ao inv√©s (veja [Sintaxe e Uso](https://github.com/vlang/v/blob/master/doc/docs.md#syntax-and-usage) e [Sele√ß√£o de Canal](https://github.com/vlang/v/blob/master/doc/docs.md#channel-select) acima).

#### Objetos Compartilhados

Os dados podem ser trocados entre uma co-rotina e o **thread** de chamada por meio de uma vari√°vel compartilhada. Essas vari√°veis devem ser criadas como **shared** e passadas para a co-rotina como tal tamb√©m. A **struct** subjacente cont√©m um **mutex** oculto que permite bloquear o acesso simult√¢neo usando **rlock** para somente leitura e bloqueio para acesso de leitura/grava√ß√£o.

```vlang
struct St {
mut:
	x int // data to shared
}

fn (shared b St) g() {
	lock b {
		// read/modify/write b.x
	}
}

fn main() {
	shared a := St{
		x: 10
	}
	go a.g()
	// ...
	rlock a {
		// read a.x
	}
}
```

Vari√°veis compartilhadas devem ser **structs**, **arrays** ou **maps**.

#### Decodificando JSON

```vlang
import json

struct Foo {
	x int
}

struct User {
	name string
	age  int
	// Use o atributo `skip` para pular certos campos
	foo Foo [skip]
	// Se o nome do campo for diferente em JSON, ele pode ser especificado
	last_name string [json: lastName]
}

data := '{ "name": "Frodo", "lastName": "Baggins", "age": 25 }'
user := json.decode(User, data) or {
	eprintln('Falha ao decodificar json')
	return
}
println(user.name)
println(user.last_name)
println(user.age)
// Voc√™ tamb√©m pode decodificar array JSON:
sfoos := '[{"x":123},{"x":456}]'
foos := json.decode([]Foo, sfoos) ?
println(foos[0].x)
println(foos[1].x)
```

Devido √† natureza onipresente do **JSON**, o suporte para ele √© integrado diretamente no V.

A fun√ß√£o **json.decode** leva dois argumentos: o primeiro √© o tipo no qual o valor **JSON** deve ser decodificado e o segundo √© uma **string** contendo os dados **JSON**.

**V** gera c√≥digo para codifica√ß√£o e decodifica√ß√£o **JSON**. Nenhuma reflex√£o de tempo de execu√ß√£o √© usada. Isso resulta em um desempenho muito melhor.

#### Testando

**Asserts**

```vlang
fn foo(mut v []int) {
	v[0] = 1
}

mut v := [20]
foo(mut v)
assert v[0] < 4
```

Uma declara√ß√£o **assert** verifica se sua express√£o √© avaliada como verdadeira. Se uma declara√ß√£o falhar, o programa ser√° abortado. **Asserts** devem ser usados apenas para detectar erros de programa√ß√£o. Quando uma declara√ß√£o falha, ela √© informada a **stderr** e os valores em cada lado de um operador de compara√ß√£o (como <, ==) ser√£o impressos quando poss√≠vel. Isso √© √∫til para encontrar facilmente um valor inesperado. As declara√ß√µes assert podem ser usadas em qualquer fun√ß√£o.

**Arquivos de teste**

```vlang
// hello.v
module main

fn hello() string {
	return 'Hello world'
}

fn main() {
	println(hello())
}
module main
// hello_test.v
fn test_hello() {
	assert hello() == 'Hello world'
}
```

Para executar o teste acima, use `v hello_test.v`. Isso verificar√° se a fun√ß√£o **hello** est√° produzindo a sa√≠da correta. V executa todas as fun√ß√µes de teste no arquivo.

- Todas as fun√ß√µes de teste devem estar dentro de um arquivo de teste cujo nome termina em _test.v.
- Os nomes das fun√ß√µes de teste devem come√ßar com test_ para marc√°-los para execu√ß√£o.
- As fun√ß√µes normais tamb√©m podem ser definidas em arquivos de teste e devem ser chamadas manualmente. Outros s√≠mbolos tamb√©m podem ser definidos em arquivos de teste, por exemplo, tipos.
- Existem dois tipos de testes: externos e internos.
- Os testes internos devem declarar seu m√≥dulo, assim como todos os outros arquivos .v do mesmo m√≥dulo. Os testes internos podem at√© chamar fun√ß√µes privadas no mesmo m√≥dulo.
- Os testes externos devem importar os m√≥dulos que eles testam. Eles n√£o t√™m acesso √†s fun√ß√µes / tipos privados dos m√≥dulos. Eles podem testar apenas a API externa / p√∫blica fornecida por um m√≥dulo.

No exemplo acima, test_hello √© um teste interno, que pode chamar a fun√ß√£o privada hello() porque hello_test.v tem o m√≥dulo principal, assim como hello.v, ou seja, ambos fazem parte do mesmo m√≥dulo. Observe tamb√©m que, como o m√≥dulo principal √© um m√≥dulo regular como os outros, os testes internos tamb√©m podem ser usados para testar fun√ß√µes privadas nos arquivos .v do seu programa principal.

Voc√™ tamb√©m pode definir fun√ß√µes de teste especiais em um arquivo de teste:

- testsuite_begin que ser√° executado antes de todas as outras fun√ß√µes de teste.
- testsuite_end que ser√° executado ap√≥s todas as outras fun√ß√µes de teste.

**Executando testes**

Para executar fun√ß√µes de teste em um arquivo de teste individual, use `v foo_test.v`.

Para testar um m√≥dulo inteiro, use `v test mymodule`. Voc√™ tamb√©m pode usar o teste v. para testar tudo dentro de sua pasta atual (e subpastas). Voc√™ pode passar a op√ß√£o `-stats` para ver mais detalhes sobre os testes individuais executados.

#### Gerenciamento de mem√≥ria

V evita fazer aloca√ß√µes desnecess√°rias em primeiro lugar usando tipos de valor, buffers de string, promovendo um estilo de c√≥digo simples livre de abstra√ß√£o.

A maioria dos objetos (~ 90-100%) s√£o liberados pelo mecanismo autofree do V: o compilador insere chamadas gratuitas necess√°rias automaticamente durante a compila√ß√£o. A pequena porcentagem restante de objetos √© liberada por meio da contagem de refer√™ncia.

O desenvolvedor n√£o precisa alterar nada em seu c√≥digo. "Ele simplesmente funciona", como em Python, Go ou Java, exceto que n√£o h√° GC pesado rastreando tudo ou RC caro para cada objeto.

**Control**

Voc√™ pode aproveitar as vantagens do mecanismo autofree do V e definir um m√©todo **free()** em tipos de dados personalizados:

```vlang
struct MyType {}

[unsafe]
fn (data &MyType) free() {
	// ...
}
```

Assim como o compilador libera tipos de dados **C** com **free()** do **C**, ele ir√° inserir estaticamente chamadas **free()** para o seu tipo de dados no final do tempo de vida de cada vari√°vel.

Para desenvolvedores que desejam ter mais controle de baixo n√≠vel, o autofree pode ser desabilitado com **-manualfree**, ou adicionando um [manualfree] em cada fun√ß√£o que deseja gerenciar sua mem√≥ria manualmente. (Veja atributos).

Nota: neste momento, o **autofree** est√° escondido atr√°s da bandeira **-autofree**. Ele ser√° habilitado por padr√£o em V 0.3. Se o **autofree** n√£o for usado, os programas em V vazar√£o mem√≥ria.

**Exemplos**

```vlang
import strings

fn draw_text(s string, x int, y int) {
	// ...
}

fn draw_scene() {
	// ...
	name1 := 'abc'
	name2 := 'def ghi'
	draw_text('hello $name1', 10, 10)
	draw_text('hello $name2', 100, 10)
	draw_text(strings.repeat(`X`, 10000), 10, 50)
	// ...
}
```

As **strings** n√£o escapam de **draw_text**, ent√£o elas s√£o limpas quando a fun√ß√£o termina.

Na verdade, com o sinalizador **-prealloc**, as duas primeiras chamadas n√£o resultar√£o em nenhuma aloca√ß√£o. Essas duas strings s√£o pequenas, ent√£o V usar√° um buffer pr√©-alocado para elas.

```vlang
struct User {
	name string
}

fn test() []int {
	number := 7 // stack variable
	user := User{} // struct allocated on stack
	numbers := [1, 2, 3] // array allocated on heap, will be freed as the function exits
	println(number)
	println(user)
	println(numbers)
	numbers2 := [4, 5, 6] // array that's being returned, won't be freed here
	return numbers2
}
```

#### ORM

(Ainda est√° em estado alfa)

V tem um ORM (mapeamento objeto-relacional) embutido que oferece suporte a SQLite e em breve oferecer√° suporte a MySQL, Postgres, MS SQL e Oracle.

ORM da V oferece uma s√©rie de benef√≠cios:

- Uma sintaxe para todos os dialetos SQL. (Migrar entre bancos de dados se torna muito mais f√°cil.)
- As consultas s√£o constru√≠das usando a sintaxe de V. (N√£o h√° necessidade de aprender outra sintaxe.)
- Seguran√ßa. (Todas as consultas s√£o automaticamente higienizadas para evitar inje√ß√£o de SQL.)
- Compile verifica√ß√µes de tempo. (Isso evita erros de digita√ß√£o que s√≥ podem ser detectados durante o tempo de execu√ß√£o.)
- Legibilidade e simplicidade. (Voc√™ n√£o precisa analisar manualmente os resultados de uma consulta e, em seguida, construir manualmente os objetos a partir dos resultados analisados.)

```vlang
import sqlite

struct Customer {
	// nome da estrutura tem que ser igual ao nome da tabela (por enquanto)
	id        int // um campo chamado `id` do tipo inteiro deve ser o primeiro campo
	name      string
	nr_orders int
	country   string
}

db := sqlite.connect('customers.db') ?
// select count(*) from Customer
nr_customers := sql db {
	select count from Customer
}
println('number of all customers: $nr_customers')
// A sintaxe V pode ser usada para construir consultas
// db.select retorna uma array
uk_customers := sql db {
	select from Customer where country == 'uk' && nr_orders > 0
}
println(uk_customers.len)
for customer in uk_customers {
	println('$customer.id - $customer.name')
}
// adicionando `limit 1`, dizemos a V que haver√° apenas um objeto
customer := sql db {
	select from Customer where id == 1 limit 1
}
println('$customer.id - $customer.name')
// insert a new customer
new_customer := Customer{
	name: 'Bob'
	nr_orders: 10
}
sql db {
	insert new_customer into Customer
}
```

Para obter mais exemplos, consulte [vlib/orm/orm_test.v](https://github.com/vlang/v/blob/master/vlib/orm/orm_test.v).

#### Escrevendo Documenta√ß√£o

A forma como funciona √© muito semelhante ao Go. √â muito simples: n√£o h√° necessidade de escrever documenta√ß√£o separadamente para seu c√≥digo, o vdoc ir√° ger√°-lo a partir de docstrings no c√≥digo-fonte.

A documenta√ß√£o para cada fun√ß√£o/tipo/const deve ser colocada logo antes da declara√ß√£o:

```vlang
// clearall limpa todos os bits no array
fn clearall() {
}
```

O coment√°rio deve come√ßar com o nome da defini√ß√£o.

√Äs vezes, uma linha n√£o √© suficiente para explicar o que uma fun√ß√£o faz; nesse caso, os coment√°rios devem se estender para a fun√ß√£o documentada usando coment√°rios de uma √∫nica linha:

```vlang
// copy_all copia recursivamente todos os elementos do array por seus valores,
// se `dupes` √© falso, todos os valores duplicados s√£o eliminados no processo.
fn copy_all(dupes bool) {
	// ...
}
```

Por conven√ß√£o, √© prefer√≠vel que os coment√°rios sejam escritos no tempo presente.

Uma vis√£o geral do m√≥dulo deve ser colocada no primeiro coment√°rio logo ap√≥s o nome do m√≥dulo.

Para gerar a documenta√ß√£o, use o vdoc, por exemplo `v doc net.http`.

#### Ferramentas

**v fmt**

Voc√™ n√£o precisa se preocupar em formatar seu c√≥digo ou definir diretrizes de estilo. v fmt cuida disso:

```vlang
v fmt file.v
```

√â recomend√°vel configurar seu editor, de modo que v fmt -w seja executado em cada salvamento. Uma execu√ß√£o vfmt geralmente √© muito barata (leva <30ms).

Sempre execute v fmt -w file.v antes de enviar seu c√≥digo.

**Profiling**

V tem um bom suporte para a cria√ß√£o de perfil de seus programas: `v -profile profile.txt run file.v` Isso produzir√° um arquivo profile.txt, que voc√™ pode analisar.

O arquivo profile.txt gerado ter√° linhas com 4 colunas: a) quantas vezes uma fun√ß√£o foi chamada b) quanto tempo no total uma fun√ß√£o levou (em ms) c) quanto tempo, em m√©dia, uma chamada para uma fun√ß√£o levou (em ns) d) o nome da fun√ß√£o v

Voc√™ pode classificar na coluna 3 (tempo m√©dio por fun√ß√£o) usando: `sort -n -k3 profile.txt|tail`

Voc√™ tamb√©m pode usar cron√¥metros para medir apenas partes do seu c√≥digo explicitamente:

```vlang
import time

fn main() {
	sw := time.new_stopwatch({})
	println('Hello world')
	println('Greeting the world took: ${sw.elapsed().nanoseconds()}ns')
}
```

### T√≥picos Avan√ßados

#### C√≥digo inseguro de mem√≥ria

√Äs vezes, para efici√™ncia, voc√™ pode querer escrever c√≥digo de baixo n√≠vel que pode corromper a mem√≥ria ou ser vulner√°vel a explora√ß√µes de seguran√ßa. V suporta escrever esse c√≥digo, mas n√£o por padr√£o.

V requer que quaisquer opera√ß√µes potencialmente inseguras para a mem√≥ria sejam marcadas intencionalmente. Marc√°-los tamb√©m indica a qualquer pessoa que esteja lendo o c√≥digo que pode haver viola√ß√µes de seguran√ßa de mem√≥ria se houver um erro.

Exemplos de opera√ß√µes potencialmente inseguras para a mem√≥ria s√£o:

- Aritm√©tica de ponteiro
- Indexa√ß√£o de ponteiro
- Convers√£o para ponteiro de um tipo incompat√≠vel
- Chamar certas fun√ß√µes C, por exemplo free, strlen e strncmp.

Para marcar opera√ß√µes potencialmente inseguras para a mem√≥ria, coloque-as em um bloco inseguro (unsafe):

```vlang
// aloca 2 bytes n√£o inicializados e retorna uma refer√™ncia a eles
mut p := unsafe { malloc(2) }
p[0] = `h` // Erro: a indexa√ß√£o do ponteiro s√≥ √© permitida em blocos `inseguros` (`unsafe`)
unsafe {
    p[0] = `h` // OK
    p[1] = `i`
}
p++ // Erro: aritm√©tica de ponteiro s√≥ √© permitida em blocos `inseguros`
unsafe {
    p++ // OK
}
assert *p == `i`
```

A pr√°tica recomendada √© evitar colocar express√µes seguras para a mem√≥ria dentro de um bloco inseguro, para que o motivo do uso de inseguro seja o mais claro poss√≠vel. Geralmente, qualquer c√≥digo que voc√™ acha que √© seguro para a mem√≥ria n√£o deve estar dentro de um bloco inseguro, para que o compilador possa verific√°-lo.

Se voc√™ suspeita que seu programa viola a seguran√ßa da mem√≥ria, voc√™ tem uma vantagem para encontrar a causa: observe os blocos inseguros (e como eles interagem com o c√≥digo ao redor).

- Nota: Este √© um trabalho em andamento.

#### Structs com campos de refer√™ncia

As estruturas com refer√™ncias requerem a configura√ß√£o expl√≠cita do valor inicial para um valor de refer√™ncia, a menos que a estrutura j√° defina seu pr√≥prio valor inicial.

Refer√™ncias de valor zero, ou ponteiros nulos, N√ÉO ser√£o suportados no futuro, por enquanto estruturas de dados como Listas Vinculadas ou √Årvores Bin√°rias que dependem de campos de refer√™ncia que podem usar o valor 0, entendendo que n√£o √© seguro e pode causar p√¢nico.

```vlang
struct Node {
	a &Node
	b &Node = 0 // Auto-inicializado como nulo, use com cuidado!
}

// Os campos de refer√™ncia devem ser inicializados, a menos que um valor inicial seja declarado.
// Zero (0) est√° OK, mas use com cuidado, √© um ponteiro nulo.
foo := Node{
	a: 0
}
bar := Node{
	a: &foo
}
baz := Node{
	a: 0
	b: 0
}
qux := Node{
	a: &foo
	b: &bar
}
println(baz)
println(qux)
```

#### sizeof e __offsetof

- sizeof (Tipo) fornece o tamanho de um tipo em bytes.
- __offsetof (Struct, field_name) fornece o deslocamento em bytes de um campo de struct.

```vlang
struct Foo {
	a int
	b int
}

assert sizeof(Foo) == 8
assert __offsetof(Foo, a) == 0
assert __offsetof(Foo, b) == 4
```

#### Chamando C de V

**Exemplo**

```vlang
#flag -lsqlite3
#include "sqlite3.h"
// Veja tamb√©m o exemplo de https://www.sqlite.org/quickstart.html
struct C.sqlite3 {
}

struct C.sqlite3_stmt {
}

type FnSqlite3Callback = fn (voidptr, int, &charptr, &charptr) int

fn C.sqlite3_open(charptr, &&C.sqlite3) int

fn C.sqlite3_close(&C.sqlite3) int

fn C.sqlite3_column_int(stmt &C.sqlite3_stmt, n int) int

// ... voc√™ tamb√©m pode apenas definir o tipo de par√¢metro e omitir o prefixo C.
fn C.sqlite3_prepare_v2(&C.sqlite3, charptr, int, &&C.sqlite3_stmt, &charptr) int

fn C.sqlite3_step(&C.sqlite3_stmt)

fn C.sqlite3_finalize(&C.sqlite3_stmt)

fn C.sqlite3_exec(db &C.sqlite3, sql charptr, cb FnSqlite3Callback, cb_arg voidptr, emsg &charptr) int

fn C.sqlite3_free(voidptr)

fn my_callback(arg voidptr, howmany int, cvalues &charptr, cnames &charptr) int {
	unsafe {
		for i in 0 .. howmany {
			print('| ${cstring_to_vstring(cnames[i])}: ${cstring_to_vstring(cvalues[i]):20} ')
		}
	}
	println('|')
	return 0
}

fn main() {
	db := &C.sqlite3(0) // isso significa `sqlite3 * db = 0`
	// passar uma string literal para uma chamada de fun√ß√£o C resulta em uma string C, n√£o uma string V
	C.sqlite3_open('users.db', &db)
	// C.sqlite3_open(db_path.str, &db)
	query := 'select count(*) from users'
	stmt := &C.sqlite3_stmt(0)
	// NB: voc√™ tamb√©m pode usar o campo `.str` de uma string V,
	// para obter sua representa√ß√£o terminada em zero no estilo C
	C.sqlite3_prepare_v2(db, query.str, -1, &stmt, 0)
	C.sqlite3_step(stmt)
	nr_users := C.sqlite3_column_int(stmt, 0)
	C.sqlite3_finalize(stmt)
	println('Existem usu√°rios $nr_users no banco de dados.')
	//
	error_msg := charptr(0)
	query_all_users := 'select * from users'
	rc := C.sqlite3_exec(db, query_all_users.str, my_callback, 7, &error_msg)
	if rc != C.SQLITE_OK {
		eprintln(cstring_to_vstring(error_msg))
		C.sqlite3_free(error_msg)
	}
	C.sqlite3_close(db)
}
```

#### Passando sinalizadores de compila√ß√£o C

Adicione diretivas #flag ao topo de seus arquivos V para fornecer sinalizadores de compila√ß√£o C como:

- -I para adicionar caminhos de pesquisa de arquivos de inclus√£o C
- -l para adicionar nomes de biblioteca C que voc√™ deseja vincular
- -L para adicionar caminhos de pesquisa de arquivos de biblioteca C
- -D para definir vari√°veis de tempo de compila√ß√£o

Voc√™ pode (opcionalmente) usar sinalizadores diferentes para alvos diferentes. Atualmente, os sinalizadores linux, darwin, freebsd e windows s√£o suportados.

NB: Cada bandeira deve ir em sua pr√≥pria linha (por enquanto)

```vlang
#flag linux -lsdl2
#flag linux -Ivig
#flag linux -DCIMGUI_DEFINE_ENUMS_AND_STRUCTS=1
#flag linux -DIMGUI_DISABLE_OBSOLETE_FUNCTIONS=1
#flag linux -DIMGUI_IMPL_API=
```

No comando de constru√ß√£o do console, voc√™ pode usar:

- -cflags para passar sinalizadores personalizados para o compilador C de backend.
- -cc para alterar o compilador backend C padr√£o.
- Por exemplo: -cc gcc-9 -cflags -fsanitize=thread.

Voc√™ pode definir uma vari√°vel de ambiente VFLAGS em seu terminal para armazenar suas configura√ß√µes -cc e -cflags, em vez de inclu√≠-las no comando build a cada vez.

**#pkgconfig**

Adicionar a diretiva #pkgconfig √© usada para informar ao compilador quais m√≥dulos devem ser usados para compilar e vincular usando os arquivos pkg-config fornecidos pelas respectivas depend√™ncias.

Desde que backticks n√£o possam ser usados em #flag e processos de spawning n√£o sejam desej√°veis por raz√µes de seguran√ßa e portabilidade, V usa sua pr√≥pria biblioteca pkgconfig que √© compat√≠vel com o freedesktop padr√£o.

Se nenhuma sinaliza√ß√£o for passada, ele adicionar√° --cflags e --libs, ambas as linhas abaixo fazem o mesmo:

```shell
#pkgconfig r_core
#pkgconfig --cflags --libs r_core
```

Os arquivos .pc s√£o pesquisados em uma lista codificada de caminhos padr√£o do pkg-config, o usu√°rio pode adicionar caminhos extras usando a vari√°vel de ambiente PKG_CONFIG_PATH. V√°rios m√≥dulos podem ser passados.

**Incluindo c√≥digo C**

Voc√™ tamb√©m pode incluir o c√≥digo C diretamente em seu m√≥dulo V. Por exemplo, digamos que seu c√≥digo C esteja localizado em uma pasta chamada 'c' dentro da pasta do m√≥dulo. Ent√£o:

- Coloque um arquivo v.mod dentro da pasta de n√≠vel superior do seu m√≥dulo (se voc√™ criou seu m√≥dulo com v new, voc√™ j√° tem o arquivo v.mod). Por exemplo:

```vlang
Module {
	name: 'mymodule',
	description: 'Meu melhor m√≥dulo envolve uma biblioteca C simples.',
	version: '0.0.1'
	dependencies: []
}
```

- Adicione estas linhas ao topo do seu m√≥dulo:

```shell
#flag -I @VROOT/c
#flag @VROOT/c/implementation.o
#include "header.h"
```

NB: @VROOT ser√° substitu√≠do por V com a pasta pai mais pr√≥xima, onde existe um arquivo v.mod. Qualquer arquivo .v ao lado ou abaixo da pasta onde est√° o arquivo v.mod pode usar #flag @ VROOT / abc para se referir a essa pasta. A pasta @VROOT tamb√©m √© anexada ao caminho de pesquisa do m√≥dulo, para que voc√™ possa importar outros m√≥dulos em seu @VROOT, apenas nomeando-os.

As instru√ß√µes acima far√£o com que V procure um arquivo .o compilado em sua pasta de m√≥dulo folder/c/implementation.o. Se V o encontrar, o arquivo .o ser√° vinculado ao execut√°vel principal, que usou o m√≥dulo. Se n√£o o encontrar, V assume que existe um arquivo @VROOT/c/implementation.c, e tenta compil√°-lo em um arquivo .o, ent√£o o usar√°.

Isso permite que voc√™ tenha um c√≥digo C, que est√° contido em um m√≥dulo V, para que sua distribui√ß√£o seja mais f√°cil. Voc√™ pode ver um exemplo m√≠nimo completo para usar o c√≥digo C em um m√≥dulo V wrapper aqui: project_with_c_code. Outro exemplo, demonstrando a passagem de estruturas de C para V e vice-versa: interoperar entre C para V para C.

**Tipos C**

Strings C normais terminadas em zero podem ser convertidas em strings V com unsafe {charptr (cstring) .vstring ()} ou se voc√™ j√° sabe seu comprimento com unsafe {charptr (cstring) .vstring_with_len (len)}.

NB: Os m√©todos .vstring () e .vstring_with_len () N√ÉO criam uma c√≥pia do cstring, ent√£o voc√™ N√ÉO deve liber√°-lo ap√≥s chamar o m√©todo .vstring (). Se voc√™ precisar fazer uma c√≥pia da string C (algumas APIs libc como getenv praticamente exigem isso, j√° que retornam ponteiros para a mem√≥ria libc interna), voc√™ pode usar cstring_to_vstring (cstring).

No Windows, as APIs C geralmente retornam as chamadas strings largas (codifica√ß√£o utf16). Eles podem ser convertidos em strings V com string_from_wide (& u16 (cwidestring)).

V tem esses tipos para facilitar a interoperabilidade com C:

- voidptr para C's void*,
- byteptr para o byte* de C e
- charptr para char* de C.
- & charptr para C's char**

Para converter um voidptr para uma refer√™ncia V, use user := &User (user_void_ptr).

voidptr tamb√©m pode ser referenciado em uma estrutura V por meio de casting: user := User (user_void_ptr).

[um exemplo de um m√≥dulo que chama o c√≥digo C de V](https://github.com/vlang/v/blob/master/vlib/v/tests/project_with_c_code/mod1/wrapper.v)

#### Declara√ß√µes C

Os identificadores C s√£o acessados com o prefixo C da mesma forma como os identificadores espec√≠ficos do m√≥dulo s√£o acessados. As fun√ß√µes devem ser declaradas novamente em V antes que possam ser usadas. Qualquer tipo C pode ser usado por tr√°s do prefixo C, mas os tipos devem ser declarados novamente em V para acessar os membros do tipo.

Para redeclarar tipos complexos, como no seguinte c√≥digo C:

```vlang
struct SomeCStruct {
	uint8_t implTraits;
	uint16_t memPoolData;
	union {
		struct {
			void* data;
			size_t size;
		};

		DataView view;
	};
};
```

membros de subestruturas de dados podem ser declarados diretamente na estrutura de conten√ß√£o conforme abaixo:

```vlang
struct C.SomeCStruct {
	implTraits  byte
	memPoolData u16
	// Esses membros fazem parte de subestruturas de dados que atualmente n√£o podem ser representadas em V.
	// Declar√°-los diretamente dessa forma √© suficiente para o acesso.
	// union {
	// struct {
	data voidptr
	size size_t
	// }
	view C.DataView
	// }
}
```

A exist√™ncia dos membros de dados √© informada a V, e eles podem ser usados sem recriar exatamente a estrutura original.

Como alternativa, voc√™ pode [incorporar](https://github.com/vlang/v/blob/master/doc/docs.md#embedded-structs) as subestruturas de dados para manter uma estrutura de c√≥digo paralela.

#### Depura√ß√£o de c√≥digo C gerado

Para depurar problemas no c√≥digo C gerado, voc√™ pode passar estes sinalizadores:

- -g - produz um execut√°vel menos otimizado com mais informa√ß√µes de depura√ß√£o nele. V ir√° for√ßar os n√∫meros de linha dos arquivos .v nos stacktraces, que o execut√°vel ir√° produzir em p√¢nico. Normalmente √© melhor passar -g, a menos que voc√™ esteja escrevendo um c√≥digo de baixo n√≠vel; nesse caso, use a pr√≥xima op√ß√£o -cg.
- -cg - produz um execut√°vel menos otimizado com mais informa√ß√µes de depura√ß√£o nele. O execut√°vel usar√° n√∫meros de linha de origem C neste caso. √â freq√ºentemente usado em combina√ß√£o com -keepc, para que voc√™ possa inspecionar o programa C gerado em caso de p√¢nico ou para que seu depurador (gdb, lldb etc.) possa mostrar o c√≥digo-fonte C gerado.
- -showcc - imprime o comando C que √© usado para construir o programa.
- -show-c-output - imprime a sa√≠da que seu compilador C produziu enquanto compilava seu programa.
- -keepc - n√£o exclui o arquivo de c√≥digo-fonte C gerado ap√≥s uma compila√ß√£o bem-sucedida. Al√©m disso, continue usando o mesmo caminho de arquivo, para que seja mais est√°vel e mais f√°cil de manter aberto em um editor / IDE.

Para obter a melhor experi√™ncia de depura√ß√£o, se voc√™ estiver escrevendo um wrapper de baixo n√≠vel para uma biblioteca C existente, voc√™ pode passar v√°rios desses sinalizadores ao mesmo tempo: v -keepc -cg -showcc yourprogram.v, em seguida, execute o depurador (gdb / lldb ) ou IDE no seu programa execut√°vel produzido.

Se voc√™ deseja apenas inspecionar o c√≥digo C gerado, sem compila√ß√£o adicional, voc√™ tamb√©m pode usar o sinalizador -o (por exemplo, -o file.c). Isso far√° com que V produza o file.c e pare.

Se quiser ver o c√≥digo-fonte C gerado para apenas uma √∫nica fun√ß√£o C, por exemplo main, voc√™ pode usar: -printfn main -o file.c.

Para ver uma lista detalhada de todos os sinalizadores que V suporta, use v help, v help build ev help build-c.

### Compila√ß√£o condicional

#### Compilar o c√≥digo de tempo

$ √© usado como um prefixo para opera√ß√µes em tempo de compila√ß√£o.

**$if**

```vlang
// Suporte para v√°rias condi√ß√µes em um ramo
$if ios || android {
	println('Running on a mobile device!')
}
$if linux && x64 {
	println('64-bit Linux.')
}
// Uso como express√£o
os := $if windows { 'Windows' } $else { 'UNIX' }
println('Using $os')
// ramos $if-$else
$if tinyc {
	println('tinyc')
} $else $if clang {
	println('clang')
} $else $if gcc {
	println('gcc')
} $else {
	println('different compiler')
}
$if test {
	println('testing')
}
// v -cg ...
$if debug {
	println('debugging')
}
// v -prod ...
$if prod {
	println('production build')
}
// v -d option ...
$if option ? {
	println('custom option')
}
```

Se voc√™ deseja que um if seja avaliado em tempo de compila√ß√£o, ele deve ser prefixado com um sinal $. No momento, ele pode ser usado para detectar um sistema operacional, compilador, plataforma ou op√ß√µes de compila√ß√£o. $ if debug √© uma op√ß√£o especial como $ if windows ou $ if x32. Se estiver usando um ifdef personalizado, voc√™ precisa da op√ß√£o $if ? {} e compilar com a op√ß√£o v -d. Lista completa de op√ß√µes integradas:

OS	                    | Compilers    | Platforms      | Other
----------------------- | ------------ | -------------- | -----
windows, linux, macos   | gcc, tinyc   | amd64, aarch64 | debug, prod, test
mac, darwin, ios,       | clang, mingw | x64, x32       | js, glibc, prealloc
android,mach, dragonfly |  msvc        | little_endian  | no_bounds_checking
gnu, hpux, haiku, qnx   | cplusplus    | big_endian     |
solaris, linux_or_macos |              |                |

**$embed_file**

```vlang
import os
fn main() {
	embedded_file := $embed_file('v.png')
	os.write_file('exported.png', embedded_file.to_string()) ?
}
```

V pode embutir arquivos arbitr√°rios no execut√°vel com a chamada de tempo de compila√ß√£o $embed_file(`<path>`). Os caminhos podem ser absolutos ou relativos ao arquivo de origem.

Quando voc√™ n√£o usa -prod, o arquivo n√£o √© incorporado. Em vez disso, ele ser√° carregado na primeira vez que seu programa chamar f.data() em tempo de execu√ß√£o, tornando mais f√°cil alterar em programas de editor externo, sem a necessidade de recompilar seu execut√°vel.

Ao compilar com -prod, o arquivo ser√° embutido em seu execut√°vel, aumentando o tamanho do bin√°rio, mas tornando-o mais autocontido e, portanto, mais f√°cil de distribuir. Nesse caso, f.data() n√£o causar√° nenhum IO e sempre retornar√° os mesmos dados.

**$tmpl para incorporar e analisar arquivos de modelo V**

V tem uma linguagem de modelo simples para modelos de texto e html, e eles podem ser facilmente incorporados via $tmpl ('path/to/template.txt'):

```vlang
fn build() string {
	name := 'Peter'
	age := 25
	numbers := [1, 2, 3]
	return $tmpl('1.txt')
}

fn main() {
	println(build())
}
```

**1.txt:**

```shell
name: @name

age: @age

numbers: @numbers

@for number in numbers
  @number
@end
```

**sa√≠da:**

```shell
name: Peter

age: 25

numbers: [1, 2, 3]

1
2
3
```

**$env**

```vlang
module main

fn main() {
	compile_time_env := $env('ENV_VAR')
	println(compile_time_env)
}
```

V pode trazer valores em tempo de compila√ß√£o a partir de vari√°veis de ambiente. $env('ENV_VAR') tamb√©m pode ser usado nas instru√ß√µes #flag e #include de n√≠vel superior: #flag linux -I $ env('JAVA_HOME')/include.

#### Arquivos espec√≠ficos do ambiente

Se um arquivo tiver um sufixo espec√≠fico do ambiente, ele ser√° compilado apenas para esse ambiente.

- .js.v => ser√° usado apenas pelo backend JS. Esses arquivos podem conter JS. c√≥digo.
- .c.v => ser√° usado apenas pelo backend C. Esses arquivos podem conter c√≥digo C..
- .x64.v => ser√° usado apenas pelo backend x64 de V.
- _nix.c.v => ser√° usado apenas em sistemas Unix (n√£o Windows).
- _ $ {os} .c.v => ser√° usado apenas no sistema operacional espec√≠fico. Por exemplo, _windows.c.v ser√° usado apenas ao compilar no Windows ou com -os windows.
- _default.c.v => ser√° usado apenas se N√ÉO houver um arquivo de plataforma mais espec√≠fico. Por exemplo, se voc√™ tem file_linux.c.v e file_default.c.v, e est√° compilando para linux, ent√£o apenas file_linux.c.v ser√° usado e file_default.c.v ser√° ignorado.

Aqui est√° um exemplo mais completo: main.v:

```vlang
module main
fn main() { println(message) }
```

main_default.c.v:

```vlang
module main
const ( message = 'Hello world' )
```

main_linux.c.v:

```vlang
module main
const ( message = 'Hello linux' )
```

main_windows.c.v:

```vlang
module main
const ( message = 'Hello windows' )
```

Com o exemplo acima:

- ao compilar para o Windows, voc√™ obter√° 'Hello windows'
- quando voc√™ compilar para o Linux, voc√™ obter√° 'Hello linux'
- ao compilar para qualquer outra plataforma, voc√™ receber√° a mensagem n√£o espec√≠fica 'Ol√°, mundo'.
- _d_customflag.v => ser√° usado apenas se voc√™ passar -d customflag para V. Isso corresponde a $ if customflag ? {}, mas para um arquivo inteiro, n√£o apenas um √∫nico bloco. customflag deve ser um identificador snake_case, n√£o pode conter caracteres arbitr√°rios (apenas letras latinas min√∫sculas + n√∫meros + _). NB: um postfix combinat√≥rio _d_customflag_linux.c.v n√£o funcionar√°. Se voc√™ precisar de um arquivo de sinalizador personalizado, que tenha c√≥digo dependente da plataforma, use o postfix _d_customflag.v e, em seguida, use blocos condicionais de tempo de compila√ß√£o dependentes de plaftorm dentro dele, ou seja, $ if linux {} etc.
- _notd_customflag.v => semelhante a _d_customflag.v, mas ser√° usado apenas se voc√™ N√ÉO passar -d customflag para V.

### Pseudo vari√°veis de tempo de compila√ß√£o

V tamb√©m d√° ao seu c√≥digo acesso a um conjunto de vari√°veis de pseudo string, que s√£o substitu√≠das no tempo de compila√ß√£o:

- @FN => substitu√≠do pelo nome da fun√ß√£o V atual
- @METHOD => substitu√≠do por ReceiverType.MethodName
- @MOD => substitu√≠do pelo nome do m√≥dulo V atual
- @STRUCT => substitu√≠do pelo nome da estrutura V atual
- @FILE => substitu√≠do pelo caminho do arquivo de origem V
- @LINE => substitu√≠do pelo n√∫mero da linha V onde aparece (como uma string).
- @COLUMN => substitu√≠do pela coluna onde aparece (como uma string).
- @VEXE => substitu√≠do pelo caminho para o compilador V
- @VHASH => substitu√≠do pelo hash de commit encurtado do compilador V (como uma string).
- @VMOD_FILE => substitu√≠do pelo conte√∫do do arquivo v.mod mais pr√≥ximo (como uma string).

Isso permite que voc√™ fa√ßa o seguinte exemplo, √∫til ao depurar/registrar/rastrear seu c√≥digo:

```vlang
eprintln('file: ' + @FILE + ' | line: ' + @LINE + ' | fn: ' + @MOD + '.' + @FN)
```

Outro exemplo, √© se voc√™ deseja incorporar a vers√£o / nome de v.mod dentro de seu execut√°vel:

```vlang
import v.vmod
vm := vmod.decode( @VMOD_FILE ) or { panic(err.msg) }
eprintln('$vm.name $vm.version\n $vm.description')
```

### Ajuste de desempenho

O c√≥digo C gerado geralmente √© r√°pido o suficiente, quando voc√™ compila seu c√≥digo com -prod. No entanto, existem algumas situa√ß√µes em que voc√™ pode querer dar dicas adicionais ao compilador, para que ele possa otimizar ainda mais alguns blocos de c√≥digo.

NB: Eles raramente s√£o necess√°rios e n√£o devem ser usados, a menos que voc√™ crie um perfil de seu c√≥digo e veja que h√° benef√≠cios significativos para eles. Para citar a documenta√ß√£o do gcc: "os programadores s√£o notoriamente ruins em prever como seus programas realmente funcionam".

[inline] - voc√™ pode marcar fun√ß√µes com [inline], de modo que o compilador C tentar√° embuti-las, o que em alguns casos pode ser ben√©fico para o desempenho, mas pode impactar o tamanho do seu execut√°vel.

[direct_array_access] - nas fun√ß√µes marcadas com [direct_array_access], o compilador ir√° traduzir as opera√ß√µes de array diretamente em opera√ß√µes de array C - omitindo a verifica√ß√£o de limites. Isso pode economizar muito tempo em uma fun√ß√£o que itera em um array, mas ao custo de tornar a fun√ß√£o insegura - a menos que os limites sejam verificados pelo usu√°rio.

if _likely_(bool expression) {isso sugere ao compilador C, que a express√£o booleana passada √© muito prov√°vel que seja verdadeira, ent√£o ela pode gerar c√≥digo assembly, com menos chance de erro de predi√ß√£o de ramifica√ß√£o. No back-end JS, isso n√£o faz nada.

if _unlikely_(bool expression) {semelhante a _prov√°vel_ (x), mas sugere que a express√£o booleana √© altamente improv√°vel. No back-end JS, isso n√£o faz nada.

#### Reflex√£o em tempo de compila√ß√£o

Ter suporte JSON integrado √© bom, mas V tamb√©m permite que voc√™ crie serializadores eficientes para qualquer formato de dados. V tem tempo de compila√ß√£o se e para constru√ß√µes:

```vlang
// TODO: n√£o totalmente implementado

struct User {
    name string
    age  int
}

// Nota: T deve receber apenas um nome de estrutura
fn decode<T>(data string) T {
	mut result := T{}
	// loop `for` em tempo de compila√ß√£o
	// T.fields fornece um array de um tipo de metadados de campo
	$for field in T.fields {
		$if field.typ is string {
			// $(string_expr) produz um identificador
			result.$(field.name) = get_string(data, field.name)
		} $else $if field.typ is int {
			result.$(field.name) = get_int(data, field.name)
		}
	}
	return result
}

// `decodificar <User>` gera:
fn decode_User(data string) User {
	mut result := User{}
	result.name = get_string(data, 'name')
	result.age = get_int(data, 'age')
	return result
}
```

#### Sobrecarga limitada do operador

```vlang
struct Vec {
	x int
	y int
}

fn (a Vec) str() string {
	return '{$a.x, $a.y}'
}

fn (a Vec) + (b Vec) Vec {
	return Vec{a.x + b.x, a.y + b.y}
}

fn (a Vec) - (b Vec) Vec {
	return Vec{a.x - b.x, a.y - b.y}
}

fn main() {
	a := Vec{2, 3}
	b := Vec{4, 5}
	mut c := Vec{1, 2}
	println(a + b) // "{6, 8}"
	println(a - b) // "{-2, -2}"
	c += a
	println(c) // "{3, 5}"
}
```

A sobrecarga do operador vai contra a filosofia de simplicidade e previsibilidade da V. Mas, uma vez que as aplica√ß√µes cient√≠ficas e gr√°ficas est√£o entre os dom√≠nios do V, a sobrecarga do operador √© um recurso importante para melhorar a legibilidade:

a.add (b).add(c.mul(d)) √© muito menos leg√≠vel do que a + b + c * d.

Para melhorar a seguran√ßa e facilidade de manuten√ß√£o, a sobrecarga do operador √© limitada:

- S√≥ √© poss√≠vel sobrecarregar +, -, *, /,%, <,>, ==,! =, <=,> = Operadores.
- == e! = s√£o gerados automaticamente pelo compilador, mas podem ser substitu√≠dos.
- Chamar outras fun√ß√µes dentro das fun√ß√µes do operador n√£o √© permitido.
- As fun√ß√µes do operador n√£o podem modificar seus argumentos.
- Ao usar os operadores < e ==, o tipo de retorno deve ser bool.
- ! =,>, <= e > = s√£o gerados automaticamente quando == e < s√£o definidos.
- Ambos os argumentos devem ter o mesmo tipo (assim como todos os operadores em V).
- Os operadores de atribui√ß√£o (* =, + =, / =, etc) s√£o gerados automaticamente quando os operadores s√£o definidos, embora devam retornar o mesmo tipo.

#### Montagem embutida

```vlang
a := 100
b := 20
mut c := 0
asm amd64 {
	mov eax, a
	add eax, b
	mov c, eax
	; =r (c) as c // output 
	; r (a) as a // input 
		r (b) as b
}
println('a: $a') // 100 
println('b: $b') // 20 
println('c: $c') // 120
```

Para obter mais exemplos, consulte [github.com/vlang/v/tree/master/vlib/v/tests/assembly/asm_test.amd64.v](https://github.com/vlang/v/tree/master/vlib/v/tests/assembly/asm_test.amd64.v)

#### Traduzindo C para V

TODO: a tradu√ß√£o de C para V estar√° dispon√≠vel em V 0.3.

V pode traduzir seu c√≥digo C em c√≥digo V leg√≠vel por humanos e gerar inv√≥lucros V em cima de bibliotecas C.

Vamos criar um programa simples test.c primeiro:

```c
#include "stdio.h"

int main() {
	for (int i = 0; i < 10; i++) {
		printf("hello world\n");
	}
        return 0;
}
```

Execute v translate test.c, e V ir√° gerar test.v:

```vlang
fn main() {
	for i := 0; i < 10; i++ {
		println('hello world')
	}
}
```

Para gerar um wrapper em cima de uma biblioteca C, use este comando:

```vlang
v wrapper c_code/libsodium/src/libsodium
```

Isso ir√° gerar um diret√≥rio libsodium com um m√≥dulo V.

Exemplo de um inv√≥lucro libsodium gerado por C2V:

[https://github.com/medvednikov/libsodium](https://github.com/medvednikov/libsodium)

Quando voc√™ deve traduzir o c√≥digo C e quando deve simplesmente chamar o c√≥digo C de V?

Se voc√™ tiver um c√≥digo C bem escrito e testado, √© claro que sempre pode simplesmente chamar esse c√≥digo C a partir de V.

Traduzir para V oferece v√°rias vantagens:

- Se voc√™ planeja desenvolver essa base de c√≥digo, agora voc√™ tem tudo em uma linguagem, que √© muito mais seguro e f√°cil de desenvolver do que C.
- A compila√ß√£o cruzada se torna muito mais f√°cil. Voc√™ n√£o precisa se preocupar com isso.
- N√£o h√° mais sinalizadores de constru√ß√£o e arquivos de inclus√£o tamb√©m.

#### Recarregando c√≥digo quente

```vlang
module main

import time
import os

[live]
fn print_message() {
	println('Hello! Modify this message while the program is running.')
}

fn main() {
	for {
		print_message()
		time.sleep(500 * time.millisecond)
	}
}
```

Construa este exemplo com v -live message.v.

As fun√ß√µes que voc√™ deseja recarregar devem ter o atributo [live] antes de sua defini√ß√£o.

No momento, n√£o √© poss√≠vel modificar os tipos durante a execu√ß√£o do programa.

Mais exemplos, incluindo um aplicativo gr√°fico: [github.com/vlang/v/tree/master/examples/hot_code_reload](https://github.com/vlang/v/tree/master/examples/hot_reload).

#### Compila√ß√£o cruzada

Para compilar o seu projeto, basta executar

```vlang
v -os windows .
```

ou

```vlang
v -os linux .
```

(A compila√ß√£o cruzada para macOS n√£o √© poss√≠vel temporariamente.)

Se voc√™ n√£o tiver nenhuma depend√™ncia C, isso √© tudo que voc√™ precisa fazer. Isso funciona mesmo ao compilar aplicativos GUI usando o m√≥dulo ui ou aplicativos gr√°ficos usando gg.

Voc√™ precisar√° instalar o Clang, LLD linker e baixar um arquivo zip com bibliotecas e arquivos de inclus√£o para Windows e Linux. V ir√° fornecer-lhe um link.

#### Scripts de shell de plataforma cruzada em V

V pode ser usado como uma alternativa ao Bash para escrever scripts de implanta√ß√£o, construir scripts, etc.

A vantagem de usar V para isso √© a simplicidade e previsibilidade da linguagem e o suporte para v√°rias plataformas. Os "scripts V" s√£o executados em sistemas do tipo Unix e tamb√©m no Windows.

Use a extens√£o de arquivo .vsh. Isso tornar√° todas as fun√ß√µes no m√≥dulo os globais (para que voc√™ possa usar mkdir() em vez de os.mkdir(), por exemplo).

Um exemplo de deploy.vsh:

```shellscript
#!/usr/bin/env -S v run
// O texto acima associa o arquivo a V em sistemas semelhantes ao Unix,
// para que possa ser executado apenas especificando o caminho para o arquivo
// uma vez que √© tornado execut√°vel usando `chmod + x`.

// Remova se compilar / sair, ignore quaisquer erros se n√£o
rmdir_all('build') or { }

// Criar build /, nunca falha porque build / n√£o existe
mkdir('build') ?

// Mova arquivos *.v para construir /
result := exec('mv *.v build/') ?
if result.exit_code != 0 {
	println(result.output)
}
// Similar to:
// files := ls('.') ?
// mut count := 0
// if files.len > 0 {
//     for file in files {
//         if file.ends_with('.v') {
//              mv(file, 'build/') or {
//                  println('err: $err')
//                  return
//              }
//         }
//         count++
//     }
// }
// if count == 0 {
//     println('No files')
// }
```

Agora voc√™ pode compilar isso como um programa V normal e obter um execut√°vel que pode ser implementado e executado em qualquer lugar: v deploy.vsh && ./deploy

Ou apenas execute-o mais como um script Bash tradicional: v execute deploy.vsh

Em plataformas do tipo Unix, o arquivo pode ser executado diretamente ap√≥s torn√°-lo execut√°vel usando chmod + x: ./deploy.vsh

#### Atributos

V possui v√°rios atributos que modificam o comportamento de fun√ß√µes e estruturas.

Um atributo √© uma instru√ß√£o do compilador especificada dentro de [] logo antes de uma declara√ß√£o de fun√ß√£o/struct/enum e se aplica apenas √† declara√ß√£o a seguir.

```vlang
// Chamar esta fun√ß√£o resultar√° em um aviso de suspens√£o de uso
[deprecated]
fn old_function() {
}

// Tamb√©m pode exibir uma mensagem de suspens√£o de uso personalizada
[deprecated: 'use new_function() instead']
fn legacy_function() {}

// As chamadas desta fun√ß√£o ser√£o embutidas.
[inline]
fn inlined_function() {
}

// A estrutura a seguir deve ser alocada no heap. Portanto, ele s√≥ pode ser usado como um
// refer√™ncia (`& Window`) ou dentro de outra refer√™ncia (` &OuterStruct {Window {...}} `).
[heap]
struct Window {
}

// V n√£o ir√° gerar esta fun√ß√£o e todas as suas chamadas se o sinalizador fornecido for falso.
// Para usar um sinalizador, use `v -d sinalizador`
[if debug]
fn foo() {
}

fn bar() {
	foo() // n√£o ser√° chamado se `-d debug` n√£o for passado
}

// As chamadas para a fun√ß√£o a seguir devem ser em blocos n√£o unsafe{}.
// verificado, a menos que voc√™ tamb√©m o envolva em blocos `unsafe{}`.
// checked, unless you also wrap it in `unsafe {}` blocks.
// Isso √© √∫til, quando voc√™ quer ter uma fun√ß√£o `[unsafe]` que
// tem verifica√ß√µes before/after de uma determinada opera√ß√£o unsafe, que ainda
// beneficie-se dos recursos de seguran√ßa do V.
[unsafe]
fn risky_business() {
	// c√≥digo que ser√° verificado, talvez verificando as pr√©-condi√ß√µes
	unsafe {
		// c√≥digo que * n√£o ser√° * verificado, como aritm√©tica de ponteiro,
		// acessando campos de uni√£o, chamando outros fns `[unsafe], etc ...
		// Normalmente, √© uma boa ideia tentar minimizar o c√≥digo empacotado
		// unsafe{} tanto quanto poss√≠vel.
		// Veja tamb√©m [c√≥digo memory-unsafe] (#memory-unsafe-code)
	}
	// c√≥digo que ser√° verificado, talvez verificando as condi√ß√µes de postagem and/or
	// mantendo invariantes
}

// O mecanismo autofree de V n√£o cuidar√° do gerenciamento de mem√≥ria nesta fun√ß√£o.
// Voc√™ ter√° a responsabilidade de liberar mem√≥ria manualmente nele.
[manualfree]
fn custom_allocations() {
}

// Somente para interoperabilidade C, diz a V que a estrutura a seguir √© definida com `estrutura typedef` em C
[typedef]
struct C.Foo {
}

// Usado no c√≥digo da API Win32 quando voc√™ precisa passar a fun√ß√£o de retorno de chamada
[windows_stdcall]
fn C.DefWindowProc(hwnd int, msg int, lparam int, wparam int)

// Apenas Windows:
// Se uma biblioteca de gr√°ficos padr√£o √© importada (ex. Gg, ui), a janela gr√°fica leva
// prioridade e nenhuma janela de console √© criada, efetivamente desabilitando as instru√ß√µes println().
// Use para criar explicitamente a janela do console. V√°lido antes de main() apenas.
[console]
fn main() {
}
```

#### Goto

V permite pular incondicionalmente para um r√≥tulo com goto. O nome do r√≥tulo deve estar contido na mesma fun√ß√£o da instru√ß√£o goto. Um programa pode ir para um r√≥tulo fora ou mais profundo do que o escopo atual. goto permite pular a inicializa√ß√£o da vari√°vel ou voltar para o c√≥digo que acessa a mem√≥ria que j√° foi liberada, portanto, requer unsafe.

```vlang
if x {
	// ...
	if y {
		unsafe {
			goto my_label
		}
	}
	// ...
}
my_label:
```

`goto` deve ser evitado, principalmente quando `for` pode ser usado em seu lugar. Instru√ß√µes `break/continue` pode ser usado para interromper um loop aninhado e n√£o corre o risco de violar a seguran√ßa da mem√≥ria.

### Ap√™ndices

#### Ap√™ndice I: Palavras-chave

V tem 41 palavras-chave reservadas (3 s√£o literais):

```vlang
as
asm
assert
atomic
break
const
continue
defer
else
embed
enum
false
fn
for
go
goto
if
import
in
interface
is
lock
match
module
mut
none
or
pub
return
rlock
select
shared
sizeof
static
struct
true
type
typeof
union
unsafe
__offsetof
```

Veja tamb√©m [Types](https://github.com/vlang/v/blob/master/doc/docs.md#types).

#### Ap√™ndice II: Operadores

Isso lista os operadores apenas para [tipos primitivos](https://github.com/vlang/v/blob/master/doc/docs.md#primitive-types).

```vlang
+    sum                    integers, floats, strings
-    difference             integers, floats
*    product                integers, floats
/    quotient               integers, floats
%    remainder              integers

~    bitwise NOT            integers
&    bitwise AND            integers
|    bitwise OR             integers
^    bitwise XOR            integers

!    logical NOT            bools
&&   logical AND            bools
||   logical OR             bools
!=   logical XOR            bools

<<   left shift             integer << unsigned integer
>>   right shift            integer >> unsigned integer


Precedence    Operator
    5             *  /  %  <<  >>  &
    4             +  -  |  ^
    3             ==  !=  <  <=  >  >=
    2             &&
    1             ||


Assignment Operators
+=   -=   *=   /=   %=
&=   |=   ^=
>>=  <<=
```

